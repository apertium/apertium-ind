! Modified by : Septina Dian Larasati (septina.larasati@gmail.com)
! Date        : 29 May 2010


! Berikut ini code yang mengatur morfotaktik untuk penganalisa morfologi bahasa indonesia.
! revisi dari alt-morphotactic-lexc8-2
! dijalankan pada lexc

! simbol- yang dipergunakan dalam code ini dideklarasikan dalam multichar_symbols
! +sesuatu merupakan tag yang ingin dimunculkan
! ^N symbol untuk nasal
! @sesuatu.sesuatu.sesuatu@ merupakan flag diacrictics
Multichar_Symbols
         +AV
         +PASS
         +UV
	 +Redup
	 +Actor
	 +Instrument
	 +Caus_kan
	 +Caus_i
	 +Appl_kan
	 +Appl_i
         +BareVerb
         +BareNoun
         +BareAdjective
         +BareEtc
	 +Verb
         +Noun
         +Adjective
         +Etc
	 ^N
	 ^NK
	 +
	 @D.KeAn@
	 @P.KeAn.Adjective@
	 @R.KeAn.Adjective@
	 @D.peN@
	 @P.peN.Actor@
	 @R.peN.Actor@
	 @P.peN.Instr@
	 @R.peN.Instr@
	 @D.kan@
	 @P.kan.Caus@
	 @R.kan.Caus@
	 @P.kan.Appl@
	 @R.kan.Appl@
	 @D.i@
	 @P.i.Caus@
	 @R.i.Caus@
	 @P.i.Appl@
	 @R.i.Appl@
	 @D.DiawaliPe@
         @P.DiawaliPe.Y@
         @R.DiawaliPe.Y@
	 @D.DicegahPeN@
         @P.DicegahPeN.Y@
         @R.DicegahPeN.Y@
         @D.PREF@
         @P.PREF.null@
         @R.PREF.null@
         @D.PREF.null@
         @P.PREF.meng@
         @R.PREF.meng@
         @D.PREF.meng@
         @P.PREF.di@
         @R.PREF.di@
         @D.PREF.di@
         @P.PREF.peng@
         @R.PREF.peng@
         @D.PREF.peng@
         @P.PREF.ber@
         @R.PREF.ber@
         @D.PREF.ber@
         @P.PREF.ter@
         @R.PREF.ter@
         @D.PREF.ter@
         @P.PREF.ke@
         @R.PREF.ke@
         @D.PREF.ke@
	 @P.PREF.Redup@
	 @R.PREF.Redup@
	 @D.PREF.Redup@
	 @P.PREF.RedupKhusus@
	 @R.PREF.RedupKhusus@
	 @D.PREF.RedupKhusus@
         @D.PREPREF@
	 @P.PREPREF.null@
         @R.PREPREF.null@
         @D.PREPREF.null@
         @P.PREPREF.meng@
         @R.PREPREF.meng@
         @D.PREPREF.meng@
         @P.PREPREF.di@
         @R.PREPREF.di@
         @D.PREPREF.di@
	 @P.PREPREF.peng@
         @R.PREPREF.peng@
         @D.PREPREF.peng@
	 @P.PREPREF.per@
         @R.PREPREF.per@
         @D.PREPREF.per@
         @P.PREPREF.ber@
         @R.PREPREF.ber@
         @D.PREPREF.ber@
	 @P.PREPREF.ter@
         @R.PREPREF.ter@
         @D.PREPREF.ter@
         @P.PREPREF.ke@
         @R.PREPREF.ke@
         @D.PREPREF.ke@
	 @P.PREPREF.Redup@
         @R.PREPREF.Redup@
         @D.PREPREF.Redup@
	 @D.SUFF@
	 @P.SUFF.null@
         @R.SUFF.null@
         @D.SUFF.null@
	 @P.SUFF.an@
         @R.SUFF.an@
         @D.SUFF.an@
	 @P.SUFF.kan@
         @R.SUFF.kan@
         @D.SUFF.kan@
	 @P.SUFF.i@
         @R.SUFF.i@
         @D.SUFF.i@
	 @P.SUFF.RedupKhusus@
         @R.SUFF.RedupKhusus@
         @D.SUFF.RedupKhusus@
         @P.STEM.Vrb@
         @R.STEM.Vrb@
         @P.STEM.Nom@
         @R.STEM.Nom@
         @P.STEM.Adj@
         @R.STEM.Adj@
         @P.STEM.Dll@
         @R.STEM.Dll@
	 @D.REDUP@
         @P.REDUP.Ditutup@
         @R.REDUP.Ditutup@
	 @D.REDUP.Ditutup@
	 
         
! untuk saat ini yang saya deklarasikan pada definitions merupakan dummy lexicon
! akan tetapi definitions dapat juga digunakan untuk inisialisasi awal regex-regex yang diperlukan
! Definitions
! Vrb = [[m a k a n]|[m a i n]|[b a c a]|[b e r i]|[d e n g a r]|[p u k u l]|[s i r a m]|[k e r j a]] ;
! Nom = [[b a t u]|[b i n g k a i]|[b u k u]|[t a n d a]|[k a i n]|[g e d u n g]|[s u r a t]|[s u r u h]|[t i n j u]|[t a r i]|[t a n i]] ;
! Adj = [[h i t a m]|[d i n g i n]|[s e p i]|[b i r u]|[k o t o r]|[p a n d a i]|[m a r a h]|[l a w a k]] ;
! Dll = [[s a t u]|[d u a]|[a t a s]|[b a w a h]|[d e p a n]|[s a m p i n g]|[s e b e l a s]] ;

! program selalu mulai dibaca dari root
! root memiliki continuation class prefiks
! prefiks akan menuju ke continuation classes yang berisi semua kemungkinan prefiks
LEXICON Root
        Prefiks ;
        
LEXICON Prefiks
        PrefiksNull ;
        PrefiksMeng ;
        PrefiksDi ;
	PrefiksPe ;
        PrefiksPeng ;
        PrefiksPer ;
        PrefiksBer ;
        PrefiksTer ;
        PrefiksKe ;
	PrefiksRedup ;
	PrefiksRedupKhusus ;

! beberapa kelas dibawah ini berisi semua kemungkinan prefiks
! flag diacritics di-set pada tahapan ini
LEXICON PrefiksNull
<"@P.PREF.null@">    Stems ;

LEXICON PrefiksMeng
<[0 .x. m e "^N"] "@P.PREF.meng@">     Stems ;
<[0 .x. m e "^N"] "@P.PREPREF.meng@">     PrefiksPer ;
<[0 .x. m e "^N"] "@P.PREPREF.meng@">     PrefiksBer ;
<[0 .x. m e "^N"] "@P.PREPREF.meng@">     PrefiksTer ;
<[0 .x. m e "^N"] "@P.PREPREF.meng@">     PrefiksKe ;
<[0 .x. m e "^N"] "@P.PREPREF.meng@">     PrefiksRedup ;

LEXICON PrefiksDi
<[0 .x. d i] "@P.PREF.di@">      Stems ;
<[0 .x. d i] "@P.PREPREF.di@">      PrefiksPer ;
<[0 .x. d i] "@P.PREPREF.di@">      PrefiksBer ;
<[0 .x. d i] "@P.PREPREF.di@">      PrefiksTer ;
<[0 .x. d i] "@P.PREPREF.di@">      PrefiksKe ;
<[0 .x. d i] "@P.PREPREF.di@">      PrefiksRedup ;

LEXICON PrefiksPe
<[0 .x. p e] "@P.PREF.pe@">      Stems ;

LEXICON PrefiksPeng
<[0 .x. p e "^N"] "@P.PREF.peng@">      Stems ;
<[0 .x. p e "^N"] "@D.PREPREF@" "@P.PREPREF.peng@">      PrefiksBer ;

LEXICON PrefiksPer
<[0 .x. p e r "+"] "@P.PREF.per@">     Stems ;

LEXICON PrefiksBer
<[0 .x. b e r "+"] "@P.PREF.ber@">     Stems ;
<[0 .x. b e r "+"] "@D.PREPREF@" "@P.PREPREF.ber@">     PrefiksKe ;
<[0 .x. b e r "+"] "@D.PREPREF@" "@P.PREPREF.ber@">     PrefiksPeng ;
<[0 .x. b e r "+"] "@D.PREPREF@" "@P.PREPREF.ber@">     PrefiksRedup ;

LEXICON PrefiksTer
<[0 .x. t e r "+"] "@P.PREF.ter@">     Stems ;
<[0 .x. t e r "+"] "@D.PREPREF@" "@P.PREPREF.ter@">     PrefiksRedup ;

LEXICON PrefiksKe
<[0 .x. k e] "@P.PREF.ke@">        Stems ;
<[0 .x. k e] "@D.PREPREF@" "@P.PREPREF.ke@">        PrefiksBer ;
<[0 .x. k e] "@D.PREPREF@" "@P.PREPREF.ke@">        PrefiksPeng ;
<[0 .x. k e] "@D.PREPREF@" "@P.PREPREF.ke@">        PrefiksRedup ;

LEXICON PrefiksRedup
<[0 .x. "^[" "[" "{" ] "@P.PREF.Redup@">     Stems ;
<[0 .x. "^[" "[" "{" ] "@D.PREPREF@" "@P.PREPREF.Redup@">     PrefiksPeng ;
<[0 .x. "^[" "[" "{" ] "@D.PREPREF@" "@P.PREPREF.Redup@">     PrefiksPer ;
<[0 .x. "^[" "[" "{" ] "@D.PREPREF@" "@P.PREPREF.Redup@">     PrefiksKe ;
<[0 .x. "^[" "[" "{" ] "@D.PREPREF@" "@P.PREPREF.Redup@">     PrefiksNull ;

LEXICON PrefiksRedupKhusus
<[0 .x. "^[" "[" "{" ] "@P.PREF.RedupKhusus@">     Stems ;


! Stems mengambil regex dummy lexicon dan menandakannya dengan flag diacritics yang berkesesuaian
! Sufiks menuju ke continuation classes yang berisi semua kemungkinan sufiks
LEXICON Stems
	Verb ;
	Noun ;
	Adjective ;
	Etc ;
! <Vrb "@P.STEM.Vrb@">    	Redup1 ;
! <Nom "@P.STEM.Nom@">	Redup1 ;
! <Adj "@P.STEM.Adj@">		Redup1 ;
! <Dll "@P.STEM.Dll@">		Redup1 ;

!---

LEXICON Verb
sokong  	 	 	 POSFlagVerb ;
hantar 	 	 	 POSFlagVerb ;

LEXICON POSFlagVerb
<"@P.STEM.Vrb@">    	Redup1 ;

LEXICON Noun
piranti 	 	 	 POSFlagNoun ;
mesej 	 	 	 POSFlagNoun ;
had 	 	 	 POSFlagNoun ;
aksara  	 	 	 POSFlagNoun ;

LEXICON POSFlagNoun
<"@P.STEM.Nom@">    	Redup1 ;

LEXICON Adjective
teks  	 	 	 POSFlagAdjective ;
lebih 	 	 	 POSFlagAdjective ;

LEXICON POSFlagAdjective
<"@P.STEM.Adj@">		Redup1 ;

LEXICON Etc
anda  	 	 	 POSFlagEtc ;
yang  	 	 	 POSFlagEtc ;

LEXICON POSFlagEtc
<"@P.STEM.Dll@">		Redup1 ;

!---

LEXICON Redup1
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"] "@R.PREF.Redup@" "@P.REDUP.Ditutup@">		Sufiks ;
															Sufiks ;

LEXICON Sufiks
	SufiksNull ;
        SufiksAn ;
	SufiksKan ;
        SufiksI ;
	

! dari sini semua prefiks ditambahkan
! juga pemberian tag dan pemeriksaan
LEXICON SufiksNull
! kata dasar
<"+BareVerb":0 "+UV":0 "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Vrb@" "@R.PREF.null@" "@P.SUFF.null@">	Redup2 ;
<"+BareNoun":0 "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Nom@" "@R.PREF.null@" "@P.SUFF.null@">			Redup2 ;
<"+BareAdjective":0 "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Adj@" "@R.PREF.null@" "@P.SUFF.null@">		Redup2 ;
<"+BareEtc":0 "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Dll@" "@R.PREF.null@" "@P.SUFF.null@">			Redup2 ;
! kata ulang sejati tanpa imbuhan
<"+BareVerb":0 "+Redup":0 "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Vrb@" "@R.PREF.Redup@" "@P.SUFF.null@">		Redup2 ;
<"+BareNoun":0 "+Redup":0 "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Nom@" "@R.PREF.Redup@" "@P.SUFF.null@">		Redup2 ;
! imbuhan meng-
<"+Verb":0 "+AV":0 "@D.PREPREF@" "@D.REDUP@" "@R.PREF.meng@" "@P.SUFF.null@">					Redup2 ;
! imbuhan meng-per-
<"+Verb":0 "+AV":0 "@D.REDUP@" ["@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREPREF.meng@" "@R.PREF.per@" "@P.SUFF.null@">		Redup2 ;
! kata ulang sebagian imbuhan meng-
<"+Verb":0 "+AV":0 "+Redup":0 "@R.REDUP.Ditutup@" "@R.STEM.Vrb@" "@R.PREPREF.meng@" "@R.PREF.Redup@" "@P.SUFF.null@">	Redup2 ;
! kata ulang kasus khusus berimbuhan meng-
<"+Verb":0 "+AV":0 "+Redup":0 "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Vrb@" "@R.PREF.RedupKhusus@" "@P.SUFF.null@">			Redup2 ;
! imbuhan di-
<"+Verb":0 "+PASS":0 "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Vrb@" "@R.PREF.di@" "@P.SUFF.null@">		Redup2 ;
! imbuhan di-per-
<"+Verb":0 "+PASS":0 "@D.REDUP@" ["@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREPREF.di@" "@R.PREF.per@" "@P.SUFF.null@">		Redup2 ;
! kata ulang sebagian imbuhan di-
<"+Verb":0 "+PASS":0 "+Redup":0 "@R.REDUP.Ditutup@" "@R.STEM.Vrb@" "@R.PREPREF.di@" "@R.PREF.Redup@" "@P.SUFF.null@">		Redup2 ;
! imbuhan pe-
<"+Noun":0 "@D.PREPREF@" "@D.REDUP@" "@R.DiawaliPe.Y@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREF.pe@" "@P.SUFF.null@">		Redup2 ;
! imbuhan peng-
<"+Noun":0 "@D.PREPREF@" "@D.REDUP@" "@D.DicegahPeN@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREF.peng@" "@P.SUFF.null@">	Redup2 ;
! kata ulang sejati imbuhan peng-
<"+Noun":0 "+Redup":0 "@D.REDUP@" "@D.DicegahPeN.Y@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREPREF.Redup@" "@R.PREF.peng@" "@P.SUFF.null@">	Redup2 ;
! imbuhan per-
<"+Verb":0 "@D.PREPREF@" "@D.REDUP@" ["@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREF.per@" "@P.SUFF.null@">		Redup2 ;
! imbuhan ber-
<"+Verb":0 "@D.PREPREF@" "@D.REDUP@" "@R.PREF.ber@" "@P.SUFF.null@">							Redup2 ;
! kata ulang sebagian imbuhan ber-
<"+Verb":0 "+Redup":0 "@R.REDUP.Ditutup@" "@R.STEM.Vrb@" "@R.PREPREF.ber@" "@R.PREF.Redup@" "@P.SUFF.null@">			Redup2 ;
! imbuhan ter-
<"+Verb":0 "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Vrb@" "@R.PREF.ter@" "@P.SUFF.null@">						Redup2 ;
<"+Adjective":0 "@D.PREPREF@" "@D.REDUP@" ["@R.STEM.Adj@"|"@R.STEM.Dll@"] "@R.PREF.ter@" "@P.SUFF.null@">		Redup2 ;
! kata ulang sebagian imbuhan ter-
<"+Verb":0 "+Redup":0 "@R.REDUP.Ditutup@" "@R.STEM.Vrb@" "@R.PREPREF.ter@" "@R.PREF.Redup@" "@P.SUFF.null@">	Redup2 ;
! imbuhan ke-
<"+Noun":0 "@D.PREPREF@" "@D.REDUP@" ["@R.STEM.Nom@"|"@R.STEM.Adj@"|"@R.STEM.Dll@"] "@R.PREF.ke@" "@P.SUFF.null@">	Redup2 ;
! kata ulang sejati imbuhan ke-
<"+Noun":0 "+Redup":0 "@D.REDUP@" ["@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREPREF.Redup@" "@R.PREF.ke@" "@P.SUFF.null@">	Redup2 ;

LEXICON SufiksAn
! imbuhan -an
<["+Noun" .x. a n] "@D.PREPREF@" "@D.REDUP@" "@R.PREF.null@" "@P.SUFF.an@">					Redup2 ;
! kata ulang sejati imbuhan -an
<["+Noun" .x. a n] "+Redup":0 "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Vrb@" "@R.PREF.Redup@" "@P.SUFF.an@">		Redup2 ;
! kata ulang berimbuhan -an
<["+Noun" .x. a n] "+Redup":0 "@D.PREPREF@" "@R.REDUP.Ditutup@" "@R.STEM.Nom@" "@R.PREF.Redup@" "@P.SUFF.an@">			Redup2 ;
<["+Verb" .x. a n] "+Redup":0 "@D.PREPREF@" "@R.REDUP.Ditutup@" "@R.STEM.Vrb@" "@R.PREF.Redup@" "@P.SUFF.an@">			Redup2 ;
! imbuhan peng-an
<["+Noun" .x. a n] "@D.PREPREF@" "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREF.peng@" "@P.SUFF.an@">					Redup2 ;
! kata ulang sejati imbuhan peng-an
<["+Noun" .x. a n] "+Redup":0 "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREPREF.Redup@" "@R.PREF.peng@" "@P.SUFF.an@">		Redup2 ;
! imbuhan peng-ber-an
<["+Noun" .x. a n] "@D.REDUP@" "@R.PREPREF.peng@" "@R.PREF.ber@" "@P.SUFF.an@">					Redup2 ;
! kata ulang sejati imbuhan per-an
<["+Noun" .x. a n] "+Redup":0 "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"] "@R.PREPREF.Redup@" "@R.PREF.per@" "@P.SUFF.an@">				Redup2 ;
! imbuhan per-an
<["+Noun" .x. a n] "@D.PREPREF@" "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Dll@"] "@R.PREF.per@" "@P.SUFF.an@">		Redup2 ;
! imbuhan ber-an
<["+Verb" .x. a n] "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Vrb@" "@R.PREF.ber@"  "@P.SUFF.an@">		Redup2 ;
! imbuhan ber-peng-an
<["+Verb" .x. a n] "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREPREF.ber@" "@R.PREF.peng@" "@P.SUFF.an@">					Redup2 ;
! imbuhan ber-ke-an
<["+Verb" .x. a n] "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Adj@"|"@R.STEM.Dll@"] "@R.PREPREF.ber@" "@R.PREF.ke@" "@P.SUFF.an@">		Redup2 ;
! kata ulang berimbuhan ber-an
<["+Verb" .x. a n] "+Redup":0 "@R.REDUP.Ditutup@" "@R.STEM.Vrb@" "@R.PREPREF.ber@" "@R.PREF.Redup@" "@P.SUFF.an@">		Redup2 ;
! imbuhan ke-an
! KeAn bisa menghasilkan tag +Adjective hanya jika pada lexicon didefinisikan sebagai Adjective yang menjadi adjective bila diberi imbuhan KeAn
! default akan menjadi Noun
<["+Noun" .x. a n] "@D.PREPREF@" "@D.REDUP@" "@D.KeAn@" ["@R.STEM.Vrb@"|"@R.STEM.Adj@"|"@R.STEM.Dll@"] "@R.PREF.ke@" "@P.SUFF.an@">		Redup2 ;
<["+Adjective" .x. a n] "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Adj@" "@R.PREF.ke@" "@R.KeAn.Adjective@" "@P.SUFF.an@">						Redup2 ;
! kata ulang sejati imbuhan ke-an
<["+Noun" .x. a n] "+Redup":0 "@D.REDUP@" "@D.KeAn@" ["@R.STEM.Vrb@"|"@R.STEM.Adj@"|"@R.STEM.Dll@"] "@R.PREPREF.Redup@" "@P.SUFF.an@">		Redup2 ;
! imbuhan ke-peng-an
<["+Noun" .x. a n] "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREPREF.ke@" "@R.PREF.peng@" "@P.SUFF.an@">			Redup2 ;
! imbuhan ke-ber-an
<["+Noun" .x. a n] "@D.REDUP@" "@R.PREPREF.ke@" "@R.PREF.ber@" "@P.SUFF.an@">					Redup2 ;
! kata ulang berimbuhan ke-an
<["+Adjective" .x. a n] "+Redup":0 "@R.REDUP.Ditutup@" "@R.STEM.Adj@" "@R.PREPREF.ke@" "@R.PREF.Redup@" "@P.SUFF.an@">				Redup2 ;

LEXICON SufiksKan
! imbuhan -kan
<["+Verb" .x. k a n] "@D.PREPREF@" "@D.REDUP@" "@R.PREF.null@" "@P.SUFF.kan@">					Redup2 ;
! imbuhan meng-kan
<["+Verb" .x. k a n] "+AV":0 "@D.PREPREF@" "@D.REDUP@" "@R.PREF.meng@" "@P.SUFF.kan@">     			Redup2 ;
! imbuhan meng-per-kan
<["+Verb" .x. k a n] "+AV":0 "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Dll@"] "@R.PREPREF.meng@" "@R.PREF.per@" "@P.SUFF.kan@">	Redup2 ;
! imbuhan meng-ber-kan
<["+Verb" .x. k a n] "+AV":0 "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREPREF.meng@" "@R.PREF.ber@" "@P.SUFF.kan@">	Redup2 ;
! imbuhan meng-ter-kan
<["+Verb" .x. k a n] "+AV":0 "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"] "@R.PREPREF.meng@" "@R.PREF.ter@" "@P.SUFF.kan@">		Redup2 ;
! imbuhan meng-ke-kan
<["+Verb" .x. k a n] "+AV":0 "@D.REDUP@" "@R.STEM.Nom@" "@R.PREPREF.meng@" "@R.PREF.ke@" "@P.SUFF.kan@">				Redup2 ;
! kata ulang berimbuhan meng-kan
<["+Verb" .x. k a n] "+AV":0 "+Redup":0 "@R.REDUP.Ditutup@" "@R.STEM.Vrb@" "@R.PREPREF.meng@" "@R.PREF.Redup@" "@P.SUFF.kan@">		Redup2 ;
! imbuhan di-kan
<["+Verb" .x. k a n] "+PASS":0 "@D.PREPREF@" "@D.REDUP@" "@R.PREF.di@" "@P.SUFF.kan@">			Redup2 ;
! imbuhan di-per-kan
<["+Verb" .x. k a n] "+PASS":0 "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Dll@"] "@R.PREPREF.di@" "@R.PREF.per@" "@P.SUFF.kan@">		Redup2 ;
! imbuhan di-ber-kan
<["+Verb" .x. k a n] "+PASS":0 "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREPREF.di@" "@R.PREF.ber@" "@P.SUFF.kan@">	Redup2 ;
! imbuhan di-ter-kan
<["+Verb" .x. k a n] "+PASS":0 "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"] "@R.PREPREF.di@" "@R.PREF.ter@" "@P.SUFF.kan@">			Redup2 ;
! imbuhan di-ke-kan
<["+Verb" .x. k a n] "+PASS":0 "@D.REDUP@" "@R.STEM.Nom@" "@R.PREPREF.di@" "@R.PREF.ke@" "@P.SUFF.kan@">				Redup2 ;
! kata ulang berimbuhan di-kan
<["+Verb" .x. k a n] "+PASS":0 "+Redup":0 "@R.REDUP.Ditutup@" "@R.STEM.Vrb@" "@R.PREPREF.di@" "@R.PREF.Redup@" "@P.SUFF.kan@">		Redup2 ;
! imbuhan per-kan
<["+Verb" .x. k a n] "@D.PREPREF@" "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Dll@"] "@R.PREF.per@" "@P.SUFF.kan@">		Redup2 ;
! imbuhan ber-kan
<["+Verb" .x. k a n] "@D.PREPREF@" "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Adj@"] "@R.PREF.ber@"  "@P.SUFF.kan@">		Redup2 ;
<["+Noun" .x. k a n] "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Nom@" "@R.PREF.ber@"  "@P.SUFF.kan@">		Redup2 ;
! imbuhan ter-kan
<["+Verb" .x. k a n] "@D.PREPREF@" "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"] "@R.PREF.ter@" "@P.SUFF.kan@">			Redup2 ;
! imbuhan ke-kan
<["+Verb" .x. k a n] "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Nom@" "@R.PREF.ke@" "@P.SUFF.kan@">		Redup2 ;

LEXICON SufiksI
! imbuhan -i
<"+Verb":i "@D.PREPREF@" "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREF.null@" "@P.SUFF.i@">		Redup2 ;
! imbuhan meng-i
<"+Verb":i "+AV":0 "@D.PREPREF@" "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREF.meng@" "@P.SUFF.i@">		Redup2 ;
! imbuhan meng-per-i
<"+Verb":i "+AV":0 "@D.REDUP@" "@R.STEM.Nom@" "@R.PREPREF.meng@" "@R.PREF.per@" "@P.SUFF.i@">		Redup2 ;
! imbuhan di-i
<"+Verb":i "+PASS":0 "@D.PREPREF@" "@D.REDUP@" ["@R.STEM.Vrb@"|"@R.STEM.Nom@"|"@R.STEM.Adj@"] "@R.PREF.di@" "@P.SUFF.i@">		Redup2 ;
! imbuhan di-per-i
<"+Verb":i "+PASS":0 "@D.REDUP@" "@R.STEM.Nom@" "@R.PREPREF.di@" "@R.PREF.per@" "@P.SUFF.i@">		Redup2 ;
! imbuhan per-i
<"+Verb":i "@D.PREPREF@" "@D.REDUP@" "@R.STEM.Nom@" "@R.PREF.per@" "@P.SUFF.i@">				Redup2 ;


! tutup kurung untuk kata ulang sejati
LEXICON Redup2
! untuk kata dasar dan imbuhan
<"@D.PREPREF.Redup@" "@D.PREF.Redup@" "@D.PREF.RedupKhusus@">		CausApplKan ;
! untuk kata ulang sebagian dan kata ulang berimbuhan
<"@R.REDUP.Ditutup@">					CausApplKan ;
! untuk kata ulang sejati dan kata ulang kasus khusus
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"] "@D.REDUP@" ["@R.PREPREF.Redup@"|"@R.PREF.Redup@"]>			CausApplKan ;
<[0 .x. "}" "%^REHYPH m e %^N" "]" "^" 2 "^]"] "@D.REDUP@" "@R.PREF.RedupKhusus@" "@R.SUFF.null@">	CausApplKan ;


! tag-tag khusus yang dapat dikeluarkan ataupun tidak diberikan di tiga kelas ini
! tag-tag tersebut dikeluarkan bila lexicon yang digunakan memiliki tanda untuk itu
LEXICON CausApplKan
<"@D.kan@">	CausApplI ;
<"+Caus_kan":0 "@R.SUFF.kan@" "@R.kan.Caus@">	CausApplI ;
<"+Appl_kan":0 "@R.SUFF.kan@" "@R.kan.Appl@">	CausApplI ;
 
LEXICON CausApplI
<"@D.i@">		ActorInstrument ;
<"+Caus_i":0 "@R.SUFF.i@" "@R.i.Caus@">	ActorInstrument ;
<"+Appl_i":0 "@R.SUFF.i@" "@R.i.Appl@">	ActorInstrument ;
 
LEXICON ActorInstrument
<"@D.peN@">		# ;
<"+Actor":0 ["@R.PREF.peng@"|"@R.PREF.pe@"|"@R.PREPREF.peng@"] "@R.peN.Actor@">	# ;
<"+Instrument":0 ["@R.PREF.peng@"|"@R.PREF.pe@"|"@R.PREPREF.peng@"] "@R.peN.Instr@">	# ;
