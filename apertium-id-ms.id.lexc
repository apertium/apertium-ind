! Indonesian Morphological Analyzer
! Modified by : Septina Dian Larasati (septina.larasati@gmail.com)
! Date        : 14 June 2010

! Morphotactic Rules

! <TAGS>, ^NASAL, and @THE.DIACRITIC.FLAG@

! Tag definitions
Multichar_Symbols
         %<v%>      ! Verb
         %<n%>      ! Noun
         %<a%>      ! Adjective
         %<num%>    ! Numerals
         %<prn%>    ! Pronoun
         %<cnjsub%> ! Subordinating Conjunction
         %<cnjcoo%> ! Coordinating Conjunction
         %<av%>     ! Active voice
         %<pv%>     ! Passive voice
         %<sg%>     ! Singular Mood
         %<pl%>     ! Plural Mood
         %<per%>    ! Definite Verb
         %<imper%>  ! imperinite Verb
         %<vbser%>  ! Verb "to be"
         %<vbhaver%>! Verb "to have"
         %<caus%>   ! Causative Transitivity
         %<appl%>   ! ApplicativeTransitivity
         %<cap%>    ! Capitals
         %<theme%>  ! ???
	 ^N
	 ^REHYPH
	 +
         @P.ASPECT.imperf@
         @P.ASPECT.perf@
         @P.CAP.on@
         @P.DERADJ.exceed@
         @P.DERADJ.manner@
         @P.DERADJ.positive@
         @P.DERADJ.sup@
         @P.DERNOUN.abstract@
         @P.DERNOUN.act@
         @P.DERNOUN.actor@
         @P.DERNOUN.bare@
         @P.DERNOUN.ent@
         @P.DERNOUN.theme@
         @P.DERNUM.car@
         @P.DERNUM.coll@
         @P.DERNUM.ord@
         @P.DERNUM.ref@
         @P.DERNUM.uni@
         @P.ENCLITIC.enc@
         @P.FLAG.bare@
         @P.FLAG.deradj@
         @P.FLAG.dernoun@
         @P.FLAG.numinfl@
         @P.FLAG.verbinfl@
         @P.MARK.bare@
         @P.MARK.ber@
         @P.MARK.ke@
         @P.MARK.on@
         @P.MARK.pen@
         @P.MARK.per@
         @P.MARK.ter@
         @P.NUM.pl@
         @P.NUM.sg@
         @P.OBJ.appl@
         @P.OBJ.caus@
         @P.PERSON.p1@
         @P.PERSON.p2@
         @P.PERSON.p3@
         @P.POS.adj@
         @P.POS.cnjcoo@
         @P.POS.cnjsub@
         @P.POS.det@
         @P.POS.n@
         @P.POS.num@
         @P.POS.prn@
         @P.POS.v@
         @P.PROCLITIC.pro@
         @P.REDUP.on@
         @P.VERBVAR.vbhaver@
         @P.VERBVAR.vbser@
         @P.VOICE.av@
         @P.VOICE.pv@

! Root node
LEXICON Root
<aku%<prn%>%<pro%>%<p1%>%<sg%>%+:ku          "@P.FLAG.verbinfl@">       VerbInflection ;
<aku%<prn%>%<pro%>%<p1%>%<sg%>%<cap%>%+:Ku   "@P.FLAG.verbinfl@">       VerbInflection ;
<kamu%<prn%>%<pro%>%<p2%>%<sg%>%+:kau        "@P.FLAG.verbinfl@">       VerbInflection ;
<kamu%<prn%>%<pro%>%<p2%>%<sg%>%<cap%>%+:Kau "@P.FLAG.verbinfl@">       VerbInflection ;
<"@P.CAP.on@">                        MainWord ;
                                                      MainWord ;


Lexicon MainWord
<"@P.FLAG.numinfl@">        NumeralInflection ;
<"@P.FLAG.deradj@">         DerivingAdjective ;
<"@P.FLAG.dernoun@">        DerivingNoun-Singular ;
<"@P.FLAG.dernoun@">        DerivingNoun-Plural ;
<"@P.FLAG.verbinfl@">       VerbInflection ;
                            Pronoun ;
                            Subordinating ;
                            Determiner ;





!!---- Prefixes ----!!

Lexicon NumeralInflection
<"@P.MARK.bare@">                                                           Numeral ;
<0:k 0:e  "@P.MARK.ke@">                                                    Numeral ;
<0:k 0:e  "@P.MARK.ke@" "@P.REDUP.on@" [0 .x. "^[" "[" "{"]>                Numeral ;
<0:b 0:er "@P.MARK.ber@">                                                   Numeral ;

Lexicon DerivingAdjective
<0:k 0:e  "@P.MARK.ke@">                                                    Noun ;
<0:k 0:e  "@P.MARK.ke@">                                                    Adjective ;
<0:t 0:er "@P.MARK.ter@">                                                   Adjective ;
<"@P.MARK.bare@">                                                           Adjective ;
<"@P.MARK.bare@""@P.REDUP.on@" [0 .x. "^[" "[" "{"]>                        Adjective ;

Lexicon DerivingNoun-Singular
<"@P.NUM.sg@">                                                              DerivingNoun-main ;

Lexicon DerivingNoun-Plural
<"@P.MARK.bare@" "@P.REDUP.on@" [0 .x. "^[" "[" "{"]>                       Verb ;
<"@P.MARK.bare@" "@P.REDUP.on@" [0 .x. "^[" "[" "{"]>                       Noun ;
<"@P.MARK.bare@" "@P.REDUP.on@" [0 .x. "^[" "[" "{"]>                       Adjective ;
<"@P.MARK.bare@" "@P.REDUP.on@" [0 .x. "^[" "[" "{"]>                       Numeral ;
<"@P.MARK.pen@" "@P.DERNOUN.actor@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" p e "^N"]>        Verb ;
<"@P.MARK.pen@" "@P.DERNOUN.actor@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" p e "^N"]>        Noun ;
<"@P.MARK.pen@" "@P.DERNOUN.actor@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" p e "^N"]>        Adjective ;
<"@P.MARK.pen@" "@P.DERNOUN.actor@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" p e "^N"]>        Numeral ;
<"@P.REDUP.on@" [0 .x. "^[" "[" "{" k e] "@P.MARK.ke@">                     Adjective ;
<"@P.REDUP.on@" [0 .x. "^[" "[" "{" p e r] "@P.MARK.per@">                  Noun ;
<"@P.REDUP.on@" [0 .x. "^[" "[" "{" p e r] "@P.MARK.per@">                  Adjective ;
<"@P.REDUP.on@" [0 .x. "^[" "[" "{" p e r] "@P.MARK.per@">                  Numeral ;

Lexicon DerivingNoun-main
<"@P.MARK.bare@">                                                          Verb ;
<"@P.MARK.bare@">                                                          Noun ;
<"@P.MARK.bare@">                                                          Adjective ;
<"@P.MARK.bare@">                                                          Numeral ;
<"@P.MARK.pen@" "@P.DERNOUN.actor@" [0 .x. p e "^N"]>                      Verb ;
<"@P.MARK.pen@" "@P.DERNOUN.actor@" [0 .x. p e "^N"]>                      Noun ;
<"@P.MARK.pen@" "@P.DERNOUN.actor@" [0 .x. p e "^N"]>                      Adjective ;
<"@P.MARK.pen@" "@P.DERNOUN.actor@" [0 .x. p e "^N"]>                      Numeral ;
<0:k 0:e  "@P.MARK.ke@">                                                   Adjective ;
<0:p 0:er "@P.MARK.per@">                                                  Noun ;
<0:p 0:er "@P.MARK.per@">                                                  Adjective ;
<0:p 0:er "@P.MARK.per@">                                                  Numeral ;


Lexicon VerbInflection
<"@P.VOICE.av@" "@P.ASPECT.perf@"   "@P.NUM.sg@">                                                            Verb ;
<"@P.VOICE.av@" "@P.ASPECT.perf@"   "@P.REDUP.on@" [0 .x. "^[" "[" "{"]>                                     Verb ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.NUM.sg@"   [0 .x. m e "^N"]>                                         Verb ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.REDUP.on@" [0 .x. m e "^N" "^[" "[" "{"]>                            Verb ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.NUM.sg@"   [0 .x. m e "^N"]>                                         Noun ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.REDUP.on@" [0 .x. m e "^N" "^[" "[" "{"]>                            Noun ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.NUM.sg@"   [0 .x. m e "^N"]>                                         Adjective ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.REDUP.on@" [0 .x. m e "^N" "^[" "[" "{"]>                            Adjective ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.NUM.sg@"   [0 .x. m e "^N"]>                                         Numeral ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.REDUP.on@" [0 .x. m e "^N" "^[" "[" "{"]>                            Numeral ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.NUM.sg@"   0:d 0:i>                                                  Verb ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.REDUP.on@" 0:d 0:i [0 .x. "^[" "[" "{"]>                             Verb ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.NUM.sg@"   0:d 0:i>                                                  Noun ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.REDUP.on@" 0:d 0:i [0 .x. "^[" "[" "{"]>                             Noun ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.NUM.sg@"   0:d 0:i>                                                  Adjective ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.REDUP.on@" 0:d 0:i [0 .x. "^[" "[" "{"]>                             Adjective ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.NUM.sg@"   0:d 0:i>                                                  Numeral ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.REDUP.on@" 0:d 0:i [0 .x. "^[" "[" "{"]>                             Numeral ;
<"@P.VOICE.pv@" "@P.ASPECT.perf@" "@P.NUM.sg@"   0:t 0:er>                                                   Verb ;
<"@P.VOICE.pv@" "@P.ASPECT.perf@" "@P.REDUP.on@" 0:t 0:er [0 .x. "^[" "[" "{"]>                              Verb ;
<"@P.VOICE.pv@" "@P.ASPECT.perf@" "@P.NUM.sg@"   0:t 0:er>                                                   Noun ;
<"@P.VOICE.pv@" "@P.ASPECT.perf@" "@P.REDUP.on@" 0:t 0:er [0 .x. "^[" "[" "{"]>                              Noun ;
<"@P.VOICE.av@" "@P.ASPECT.perf@" "@P.VERBVAR.vbser@" "@P.NUM.sg@"   0:p 0:er>                               Adjective ;
<"@P.VOICE.av@" "@P.ASPECT.perf@" "@P.VERBVAR.vbser@" "@P.REDUP.on@" 0:p 0:er>                               Adjective ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.VERBVAR.vbser@" "@P.NUM.sg@"   0:m 0:emper>                          Adjective ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.VERBVAR.vbser@" "@P.REDUP.on@" 0:m 0:emper [0 .x. "^[" "[" "{"]>     Adjective ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.VERBVAR.vbser@" "@P.NUM.sg@"   0:m 0:emper>                          Verb ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.VERBVAR.vbser@" "@P.REDUP.on@" 0:m 0:emper [0 .x. "^[" "[" "{"]>     Verb ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.VERBVAR.vbser@" "@P.NUM.sg@"   0:d 0:iper>                           Adjective ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.VERBVAR.vbser@" "@P.REDUP.on@" 0:d 0:iper [0 .x. "^[" "[" "{"]>      Adjective ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.VERBVAR.vbser@" "@P.NUM.sg@"   0:d 0:iper>                           Verb ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.VERBVAR.vbser@" "@P.REDUP.on@" 0:d 0:iper [0 .x. "^[" "[" "{"]>      Verb ;
<"@P.VOICE.av@" "@P.ASPECT.perf@" "@P.VERBVAR.vbhaver@" "@P.NUM.sg@"   0:b 0:er "@P.FLAG.dernoun@">          DerivingNoun-Singular ;
<"@P.VOICE.av@" "@P.ASPECT.perf@" "@P.VERBVAR.vbhaver@" "@P.NUM.sg@"   0:b 0:er "@P.FLAG.dernoun@">          DerivingNoun-Plural ;



!!---- Suffixes ----!!
Lexicon InflectionOption
<"@R.FLAG.numinfl@">        NumeralInflectionEnding ;
<"@R.FLAG.deradj@">         DerivingAdjectiveEnding ;
<"@R.FLAG.dernoun@">        DerivingNounEnding ;
<"@R.FLAG.verbinfl@">       VerbInflectionEnding ;
<"@D.FLAG@">                Tagging ;


Lexicon NumeralInflectionEnding
<      "@D.REDUP@" "@R.MARK.bare@" "@P.DERNUM.car@">            Tagging ;
<      "@D.REDUP@" "@R.MARK.ke@"   "@P.DERNUM.ord@">            Tagging ;
<0:nya "@D.REDUP@" "@R.MARK.ke@"   "@P.DERNUM.ref@">            Tagging ;
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@C.REDUP@" "@R.MARK.ke@" "@P.DERNUM.ref@" 0:nya>        Tagging ;
<      "@D.REDUP@" "@R.MARK.ber@" "@P.DERNUM.coll@">            Tagging ;

Lexicon DerivingAdjectiveEnding
<0:an "@D.REDUP@" "@R.MARK.ke@"   "@R.POS.n@"   "@P.DERADJ.manner@">         Tagging ;
<0:an "@D.REDUP@" "@R.MARK.ke@"   "@R.POS.adj@" "@P.DERADJ.exceed@">         Tagging ;
<     "@D.REDUP@" "@R.MARK.ter@"  "@R.POS.adj@" "@P.DERADJ.sup@">            Tagging ;
<     "@D.REDUP@" "@R.MARK.bare@" "@R.POS.adj@" "@P.DERADJ.positive@">       Tagging ;
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@C.REDUP@" "@P.NUM.pl@""@R.MARK.bare@" "@R.POS.adj@" "@P.DERADJ.positive@">      Tagging ;

Lexicon DerivingNounEnding
<     "@R.MARK.bare@" "@R.POS.n@" "@P.DERNOUN.bare@">   LastReduplicationCheck ;
<0:an "@R.MARK.bare@" "@P.DERNOUN.ent@">                LastReduplicationCheck ;
<0:an "@R.MARK.pen@"   "@P.DERNOUN.act@">               LastReduplicationCheck ;
<     "@R.MARK.pen@">                                   LastReduplicationCheck ;
<0:an "@R.MARK.ke@"    "@P.DERNOUN.abstract@">          LastReduplicationCheck ;
<0:an "@R.MARK.per@"   "@P.DERNOUN.theme@">             LastReduplicationCheck ;


Lexicon VerbInflectionEnding
<"@D.REDUP@">                                                                            VerbInflectionEnding-main ;
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@C.REDUP@" "@P.NUM.pl@">        VerbInflectionEnding-main ;
!<[0 .x. "%^REHYPH" ">>"  ] "@R.REDUP.on@" "@C.REDUP@" "@P.NUM.pl@">                     VerbInflectionEnding-main ;

Lexicon VerbInflectionEnding-main
<0:kan "@P.OBJ.caus@">            Tagging ;
<0:i   "@P.OBJ.appl@">            Tagging ;
                                  Tagging ;

Lexicon LastReduplicationCheck
<"@D.REDUP@">        Tagging ;
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@C.REDUP@" "@P.NUM.pl@">        LastReduplicationCheck ;






!!---- Tagging ----!!
Lexicon Tagging
<[%<v%> .x. 0]      "@R.POS.v@">                   CheckVoice ;
<[%<n%> .x. 0]      "@R.POS.n@">                   CheckVoice ;
<[%<adj%> .x. 0]    "@R.POS.adj@">	           CheckVoice ;
<[%<num%> .x. 0]    "@R.POS.num@">	           CheckVoice ;
<[%<prn%> .x. 0]    "@R.POS.prn@">		   CheckVoice ;
<[%<cnjsub%> .x. 0] "@R.POS.cnjsub@">              CheckVoice ;
<[%<cnjcoo%> .x. 0] "@R.POS.cnjcoo@">	           CheckVoice ;
<[%<det%> .x. 0]    "@R.POS.det@">	           CheckVoice ;

Lexicon CheckVoice
<[%<av%> .x. 0] "@R.VOICE.av@">		           CheckAspect ;
<[%<pv%> .x. 0] "@R.VOICE.pv@">                    CheckAspect ;
<"@D.VOICE@">                                      CheckAspect ;

Lexicon CheckAspect
<[%<perf%> .x. 0] "@R.ASPECT.perf@">		   CheckVerbVariance ;
<[%<imp%> .x. 0] "@R.ASPECT.imperf@">		   CheckVerbVariance ;
<"@D.ASPECT@">                                     CheckVerbVariance ;

Lexicon CheckVerbVariance
<[%<vbser%> .x. 0]   "@R.VERBVAR.vbser@">          CheckTransitivity ;
<[%<vbhaver%> .x. 0] "@R.VERBVAR.vbhaver@">        CheckTransitivity ;
<"@D.VERBVAR@">                                    CheckTransitivity ;


Lexicon CheckTransitivity
<[%<caus%> .x. 0] "@R.OBJ.caus@">		   CheckDerNoun ;
<[%<appl%> .x. 0] "@R.OBJ.appl@">		   CheckDerNoun ;
<"@D.OBJ@">                                        CheckDerNoun ;


Lexicon CheckDerNoun
<[%<ent%> .x. 0]      "@R.DERNOUN.ent@">	   CheckDerAdj ;
<[%<act%> .x. 0]      "@R.DERNOUN.act@">	   CheckDerAdj ;
<[%<actor%> .x. 0]    "@R.DERNOUN.actor@">	   CheckDerAdj ;
<[%<theme%> .x. 0]    "@R.DERNOUN.theme@">	   CheckDerAdj ;
<[%<abstract%> .x. 0] "@R.DERNOUN.abstract@">      CheckDerAdj ;
<[%<bare%> .x. 0]     "@R.DERNOUN.bare@">          CheckDerAdj ;
<"@D.DERNOUN@">                                    CheckDerAdj ;

Lexicon CheckDerAdj
<[%<positive%> .x. 0] "@R.DERADJ.positive@">	   CheckDerNum ;
<[%<sup%> .x. 0]      "@R.DERADJ.sup@">	           CheckDerNum ;
<[%<manner%> .x. 0]   "@R.DERADJ.manner@">	   CheckDerNum ;
<[%<exceed%> .x. 0]   "@R.DERADJ.exceed@">	   CheckDerNum ;
<"@D.DERADJ@">                                     CheckDerNum ;

Lexicon CheckDerNum
<[%<car%>  .x. 0] "@R.DERNUM.car@">                CheckPerson ;
<[%<ord%>  .x. 0] "@R.DERNUM.ord@">                CheckPerson ;
<[%<coll%> .x. 0] "@R.DERNUM.coll@">               CheckPerson ;
<[%<ref%>  .x. 0] "@R.DERNUM.ref@">                CheckPerson ;
<[%<uni%>  .x. 0] "@R.DERNUM.uni@">                CheckPerson ;
<"@D.DERNUM@">                                     CheckPerson ;

Lexicon CheckPerson
<[%<p1%> .x. 0] "@R.PERSON.p1@">                   CheckNum ;
<[%<p2%> .x. 0] "@R.PERSON.p2@">                   CheckNum ;
<[%<p3%> .x. 0] "@R.PERSON.p3@">                   CheckNum ;
<"@D.PERSON@">                                     CheckNum ;


Lexicon CheckNum
<[%<sg%>   .x. 0] "@R.NUM.sg@">                    CheckCaps ;
<[%<pl%>   .x. 0] "@R.NUM.pl@">                    CheckCaps ;
<"@D.NUM@">                                        CheckCaps ;

Lexicon CheckCaps
<[%<cap%> .x. 0] "@R.CAP.on@">                     CheckEnClitic ;
<"@D.CAP@">                                        CheckEnClitic ;

Lexicon CheckEnClitic
<[%+aku%<prn%>%<enc%>%<p1%>%<sg%>%<pos%> .x. ku]  "@R.DERNOUN@">              # ;
<[%+kamu%<prn%>%<enc%>%<p2%>%<sg%>%<pos%> .x. mu] "@R.DERNOUN@">              # ;
<[%+kamu%<prn%>%<enc%>%<p2%>%<sg%>%<pos%> .x. kau] "@R.DERNOUN@">             # ;
<[%+dia%<prn%>%<enc%>%<p3%>%<sg%>%<pos%> .x. nya] "@R.DERNOUN@">              # ;
<[%+dia%<prn%>%<enc%>%<p3%>%<sg%>%<pos%>%<cap%> .x. "-" Nya] "@R.DERNOUN@">   # ;
<[%+aku%<prn%>%<enc%>%<p1%>%<sg%> .x. ku]  "@R.VOICE@">                       # ;
<[%+kamu%<prn%>%<enc%>%<p2%>%<sg%> .x. mu] "@R.VOICE@">                       # ;
<[%+kamu%<prn%>%<enc%>%<p2%>%<sg%> .x. kau] "@R.VOICE@">                      # ;
<[%+dia%<prn%>%<enc%>%<p3%>%<sg%> .x. nya] "@R.VOICE@">                       # ;
<[%+dia%<prn%>%<enc%>%<p3%>%<sg%>%<pos%>%<cap%> .x. "-" Nya] "@R.VOICE@">     # ;
                                                                              # ;


!!---- Dictionary ----!!


LEXICON Verb
<t ulis "@P.POS.v@">        InflectionOption ;
<k irim "@P.POS.v@">        InflectionOption ;
<d ukung "@P.POS.v@">        InflectionOption ;
<l ari "@P.POS.v@">        InflectionOption ;
<a bid "@P.POS.v@">        InflectionOption ;
<a bsen "@P.POS.v@">        InflectionOption ;
<a bsorbir "@P.POS.v@">        InflectionOption ;
<a bstain "@P.POS.v@">        InflectionOption ;
<a bu "@P.POS.v@">        InflectionOption ;
<a bus "@P.POS.v@">        InflectionOption ;
<a cah "@P.POS.v@">        InflectionOption ;
<a cap "@P.POS.v@">        InflectionOption ;
<a cara "@P.POS.v@">        InflectionOption ;
<a cu "@P.POS.v@">        InflectionOption ;
<a cum "@P.POS.v@">        InflectionOption ;
<a cung "@P.POS.v@">        InflectionOption ;
<a da "@P.POS.v@">        InflectionOption ;
<a dalah "@P.POS.v@">        InflectionOption ;
<a dang "@P.POS.v@">        InflectionOption ;
<a dapun "@P.POS.v@">        InflectionOption ;
<a dar "@P.POS.v@">        InflectionOption ;
<a das "@P.POS.v@">        InflectionOption ;
<a dat "@P.POS.v@">        InflectionOption ;
<a du "@P.POS.v@">        InflectionOption ;
<a gah "@P.POS.v@">        InflectionOption ;
<a gan "@P.POS.v@">        InflectionOption ;
<a gih "@P.POS.v@">        InflectionOption ;
<a juk "@P.POS.v@">        InflectionOption ;
<a jur "@P.POS.v@">        InflectionOption ;
<a lah "@P.POS.v@">        InflectionOption ;
<a lih "@P.POS.v@">        InflectionOption ;
<a mbak "@P.POS.v@">        InflectionOption ;
<a mbal "@P.POS.v@">        InflectionOption ;
<a mbau "@P.POS.v@">        InflectionOption ;
<a mbil "@P.POS.v@">        InflectionOption ;
<a mblas "@P.POS.v@">        InflectionOption ;
<a mblek "@P.POS.v@">        InflectionOption ;
<a mbles "@P.POS.v@">        InflectionOption ;
<a mbrol "@P.POS.v@">        InflectionOption ;
<a mbruk "@P.POS.v@">        InflectionOption ;
<a mbuh "@P.POS.v@">        InflectionOption ;
<a mbul "@P.POS.v@">        InflectionOption ;
<a mbung "@P.POS.v@">        InflectionOption ;
<a mbyar "@P.POS.v@">        InflectionOption ;
<a merta "@P.POS.v@">        InflectionOption ;
<a mpai "@P.POS.v@">        InflectionOption ;
<a mprok "@P.POS.v@">        InflectionOption ;
<a mpu "@P.POS.v@">        InflectionOption ;
<a ncai "@P.POS.v@">        InflectionOption ;
<a ndak "@P.POS.v@">        InflectionOption ;
<a ndam "@P.POS.v@">        InflectionOption ;
<a ndan "@P.POS.v@">        InflectionOption ;
<a ndok "@P.POS.v@">        InflectionOption ;
<a nduh "@P.POS.v@">        InflectionOption ;
<a nggar "@P.POS.v@">        InflectionOption ;
<a nggit "@P.POS.v@">        InflectionOption ;
<a ngglap "@P.POS.v@">        InflectionOption ;
<a nggul "@P.POS.v@">        InflectionOption ;
<a nggung "@P.POS.v@">        InflectionOption ;
<a nggung "-" anggip "@P.POS.v@">        InflectionOption ;
<a ngka "@P.POS.v@">        InflectionOption ;
<a ngkat "@P.POS.v@">        InflectionOption ;
<a ngkit "@P.POS.v@">        InflectionOption ;
<a ngkut "@P.POS.v@">        InflectionOption ;
<a ngler "@P.POS.v@">        InflectionOption ;
<a ngon "@P.POS.v@">        InflectionOption ;
<a ngop "@P.POS.v@">        InflectionOption ;
<a ngsur "@P.POS.v@">        InflectionOption ;
<a njak "@P.POS.v@">        InflectionOption ;
<a njal "@P.POS.v@">        InflectionOption ;
<a njang "@P.POS.v@">        InflectionOption ;
<a njlok "@P.POS.v@">        InflectionOption ;
<a njur "@P.POS.v@">        InflectionOption ;
<a ntar "@P.POS.v@">        InflectionOption ;
<a ntih "@P.POS.v@">        InflectionOption ;
<a ntre "@P.POS.v@">        InflectionOption ;
<a nut "@P.POS.v@">        InflectionOption ;
<a nyam "@P.POS.v@">        InflectionOption ;
<a nyang "@P.POS.v@">        InflectionOption ;
<a pel "@P.POS.v@">        InflectionOption ;
<a pkir "@P.POS.v@">        InflectionOption ;
<a plus "@P.POS.v@">        InflectionOption ;
<a prit "@P.POS.v@">        InflectionOption ;
<a pung "@P.POS.v@">        InflectionOption ;
<a rak "@P.POS.v@">        InflectionOption ;
<a ras "@P.POS.v@">        InflectionOption ;
<a rik "@P.POS.v@">        InflectionOption ;
<a rsir "@P.POS.v@">        InflectionOption ;
<a run "@P.POS.v@">        InflectionOption ;
<a sa "@P.POS.v@">        InflectionOption ;
<a sah "@P.POS.v@">        InflectionOption ;
<a sese "@P.POS.v@">        InflectionOption ;
<a suh "@P.POS.v@">        InflectionOption ;
<a sung "@P.POS.v@">        InflectionOption ;
<a teret "@P.POS.v@">        InflectionOption ;
<a top "@P.POS.v@">        InflectionOption ;
<a tret "@P.POS.v@">        InflectionOption ;
<a tur "@P.POS.v@">        InflectionOption ;
<a wai "@P.POS.v@">        InflectionOption ;
<a walengas "@P.POS.v@">        InflectionOption ;
<a was "@P.POS.v@">        InflectionOption ;
<a wat "@P.POS.v@">        InflectionOption ;
<b aka "@P.POS.v@">        InflectionOption ;

LEXICON Noun
<b uku "@P.POS.n@">         InflectionOption ;
<t oko "@P.POS.n@">         InflectionOption ;
<k arakter "@P.POS.n@">         InflectionOption ;
<b atas "@P.POS.n@">         InflectionOption ;
<t eks "@P.POS.n@">         InflectionOption ;
<p erangkat "@P.POS.n@">         InflectionOption ;
<p esan "@P.POS.n@">         InflectionOption ;


LEXICON Adjective
<b esar "@P.POS.adj@">        InflectionOption ;
<l ebih "@P.POS.adj@">        InflectionOption ;

LEXICON Numeral
<s atu  "@P.POS.num@">        InflectionOption ;
<d ua  "@P.POS.num@">        InflectionOption ;
<t iga "@P.POS.num@">        InflectionOption ;


LEXICON Pronoun
<s aya "@P.POS.prn@" "@P.PERSON.p1@" "@P.NUM.sg@">       InflectionOption ;
<a ku "@P.POS.prn@" "@P.PERSON.p1@" "@P.NUM.sg@">        InflectionOption ;
<a nda "@P.POS.prn@" "@P.PERSON.p2@" "@P.NUM.sg@">       InflectionOption ;
<k amu "@P.POS.prn@" "@P.PERSON.p2@" "@P.NUM.sg@">       InflectionOption ;
<k amu:au "@P.POS.prn@" "@P.PERSON.p2@" "@P.NUM.sg@">    InflectionOption ;
<d ia "@P.POS.prn@" "@P.PERSON.p3@" "@P.NUM.sg@">        InflectionOption ;
<k ami "@P.POS.prn@" "@P.PERSON.p1@" "@P.NUM.pl@">       InflectionOption ;
<k ita "@P.POS.prn@" "@P.PERSON.p1@" "@P.NUM.pl@">       InflectionOption ;
<k alian "@P.POS.prn@" "@P.PERSON.p2@" "@P.NUM.pl@">     InflectionOption ;
<m ereka "@P.POS.prn@" "@P.PERSON.p3@" "@P.NUM.pl@">     InflectionOption ;

LEXICON Subordinating
<y ang "@P.POS.cnjsub@">        InflectionOption ;

LEXICON Determiner
<i ni "@P.POS.det@">            InflectionOption ;


