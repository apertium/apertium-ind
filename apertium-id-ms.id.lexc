! Indonesian Morphological Analyzer
! Modified by : Septina Dian Larasati (septina.larasati@gmail.com)
! Date        : 04 June 2010

! Morphotactic Rules

! <TAGS>, ^NASAL, and @THE.DIACRITIC.FLAG@

! Tag definitions
Multichar_Symbols
         %<v%>      ! Verb
         %<n%>      ! Noun
         %<a%>      ! Adjective
         %<num%>    ! Numerals
         %<prn%>    ! Pronoun
         %<cnjsub%> ! Subordinating Conjunction
         %<cnjcoo%> ! Coordinating Conjunction
         %<av%>     ! Active voice
         %<pv%>     ! Passive voice
         %<sg%>     ! Singular Mood
         %<pl%>     ! Plural Mood
         %<per%>    ! Definite Verb
         %<imper%>  ! imperinite Verb
         %<vbser%>  ! Verb "to be"
         %<vbhaver%>! Verb "to have"
         %<caus%>   ! Causative Transitivity
         %<appl%>   ! ApplicativeTransitivity
         %<cap%>    ! Capitals
         %<theme%>  ! ???
	 ^N
	 ^REHYPH
	 ^CAP
	 +
	 @P.VOICE.av@
	 @P.VOICE.pv@
	 @P.VERB.per@
	 @P.VERB.imper@
	 @P.VERBVAR.vbser@
	 @P.VERBVAR.vbhaver@
	 @P.POS.v@
	 @P.POS.n@
	 @P.POS.adj@
	 @P.POS.num@
	 @P.POS.prn@
	 @P.POS.cnjsub@
	 @P.POS.cnjcoo@
	 @P.NUM.sg@
	 @P.NUM.pl@
	 @P.NUM.coll@
	 @P.NUM.ord@
	 @P.NUM.car@
	 @P.ADJ.elev@
	 @P.ADJ.sup@
	 @P.REDUP.on@
	 @P.REDUP.onsuff@
	 @P.NOUN.ent@
	 @P.NOUN.theme@
	 @P.NOUN.act@
	 @P.NOUN.ger@
	 @P.OBJ.caus@
	 @P.OBJ.appl@
	 @P.SUFF.an@
	 @P.SUFF.kan@
	 @P.SUFF.i@
	 @P.SUFF.null@
	 @P.PREPREF.null@
	 @P.PREPREF.act@
	 @P.PREPREF.pass@
	 @P.PREPREF.dernoun@
	 @P.PREF.null@
	 @P.PREF.ber@
	 @P.PREF.per@
	 @P.CAP.on@


! Root node
LEXICON Root
       PREPREF-Null ;
       PREPREF-Active ;
       PREPREF-Passive ;
       PREPREF-DerNoun ;
<"@P.REDUP.on@" [0 .x. "^[" "[" "{" ]>       PREPREF-DerNoun ;
<"@P.REDUP.onsuff@" [0 .x. "^[" "[" "{" ]>   PREPREF-DerNoun ;

LEXICON PREPREF-Null
<"@P.PREPREF.null@">      PREF-Null ;
<"@P.PREPREF.null@">      PREF-Ber ;
<"@P.PREPREF.null@">      PREF-Per ;
<"@P.PREPREF.null@" "@P.SUFF.an@">      PREF-Per ;
<"@P.PREPREF.null@" "@P.SUFF.an@" "@P.REDUP.onsuff@" [0 .x. "^[" "[" "{" ]>      PREF-Per ;
<"@P.PREPREF.null@" "@P.SUFF.kan@">      PREF-Per ;

LEXICON PREPREF-Active
<"@P.PREPREF.act@"  "@P.VOICE.av@" "@P.VERB.imper@" [0 .x. m e "^N"]>      PREF-Null ;

LEXICON PREPREF-Passive
<0:di "@P.PREPREF.pass@"  "@P.VOICE.pv@" "@P.VERB.imper@">                 PREF-Null ;

LEXICON PREPREF-DerNoun
<"@P.PREPREF.dernoun@" "@P.NUM.sg@" "@P.NOUN.act@" [0 .x. p e "^N"]>                    PREF-Null ;

LEXICON PREF-Null
<"@R.PREPREF.null@" "@P.PREF.null@" "@P.VOICE.av@" "@P.VERB.per@">                                           Verb ;
<"@R.PREPREF.null@" "@P.PREF.null@" "@P.VOICE.av@" "@P.VERB.per@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" ]>      Verb ;
<"@R.PREPREF.null@" "@P.PREF.null@" "@P.VOICE.av@" "@P.VERB.per@">                                           Verb2 ;
<"@R.PREPREF.null@" "@P.PREF.null@" "@P.VOICE.av@" "@P.VERB.per@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" ]>      Verb2 ;
<"@R.PREPREF.null@" "@P.PREF.null@" "@P.NUM.sg@">                                                            Noun ;
<"@R.PREPREF.null@" "@P.PREF.null@" "@P.NUM.sg@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" ]>                       Noun ;
<"@R.PREPREF.null@" "@P.PREF.null@">                                                                         Adjective ;
<"@R.PREPREF.null@" "@P.PREF.null@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" ]>                                    Adjective ;
<"@R.PREPREF.null@" "@P.PREF.null@">                                                                         Numeral ;
<"@R.PREPREF.null@" "@P.PREF.null@" "@P.SUFF.an@">                                                           Verb ;
<"@R.PREPREF.null@" "@P.PREF.null@" "@P.SUFF.an@" "@P.REDUP.onsuff@" [0 .x. "^[" "[" "{" ]>                  Verb ;

<"@R.PREPREF.null@" "@P.PREF.null@" ["@P.SUFF.kan@"|"@P.SUFF.i@"] "@P.VOICE.av@" "@P.VERB.per@" "@P.VERBVAR.vbser@">        Verb ;
<"@R.PREPREF.null@" "@P.PREF.null@" ["@P.SUFF.kan@"|"@P.SUFF.i@"] "@P.VOICE.av@" "@P.VERB.per@" "@P.VERBVAR.vbser@">        Verb2 ;
<"@R.PREPREF.null@" "@P.PREF.null@" ["@P.SUFF.kan@"|"@P.SUFF.i@"] "@P.VOICE.av@" "@P.VERB.per@" "@P.VERBVAR.vbser@">        Noun ;
<"@R.PREPREF.null@" "@P.PREF.null@" ["@P.SUFF.kan@"|"@P.SUFF.i@"] "@P.VOICE.av@" "@P.VERB.per@" "@P.VERBVAR.vbser@">        Adjective ;
<"@R.PREPREF.null@" "@P.PREF.null@" ["@P.SUFF.kan@"|"@P.SUFF.i@"] "@P.VOICE.av@" "@P.VERB.per@" "@P.VERBVAR.vbser@">        Numeral ;

<"@R.PREPREF.act@" "@P.PREF.null@" ["@P.SUFF.kan@" | "@P.SUFF.i@" | "@P.SUFF.null@"]>                        Verb ;
<"@R.PREPREF.act@" "@P.PREF.null@" ["@P.SUFF.kan@" | "@P.SUFF.i@" | "@P.SUFF.null@"]>                        Noun ;
<"@R.PREPREF.act@" "@P.PREF.null@" ["@P.SUFF.kan@" | "@P.SUFF.i@" | "@P.SUFF.null@"]>                        Adjective ;
<"@R.PREPREF.act@" "@P.PREF.null@" ["@P.SUFF.kan@" | "@P.SUFF.i@" | "@P.SUFF.null@"]>                        Numeral ;

<"@R.PREPREF.pass@" "@P.PREF.null@" ["@P.SUFF.kan@" | "@P.SUFF.i@" | "@P.SUFF.null@"]>                       Verb ;
<"@R.PREPREF.pass@" "@P.PREF.null@" ["@P.SUFF.kan@" | "@P.SUFF.i@" | "@P.SUFF.null@"]>                       Noun ;
<"@R.PREPREF.pass@" "@P.PREF.null@" ["@P.SUFF.kan@" | "@P.SUFF.i@" | "@P.SUFF.null@"]>                       Adjective ;
<"@R.PREPREF.pass@" "@P.PREF.null@" ["@P.SUFF.kan@" | "@P.SUFF.i@" | "@P.SUFF.null@"]>                       Numeral ;

<"@R.PREPREF.act@" "@P.PREF.null@" "@P.SUFF.kan@">                                                           Verb2 ;

<"@R.PREPREF.dernoun@" "@P.PREF.null@" ["@P.SUFF.an@" | "@P.SUFF.null@"]>                                    Verb ;
<"@R.PREPREF.dernoun@" "@P.PREF.null@" ["@P.SUFF.an@" | "@P.SUFF.null@"]>                                    Verb2 ;
<"@R.PREPREF.dernoun@" "@P.PREF.null@" ["@P.SUFF.an@" | "@P.SUFF.null@"]>                                    Noun ;
<"@R.PREPREF.dernoun@" "@P.PREF.null@" ["@P.SUFF.an@" | "@P.SUFF.null@"]>                                    Adjective ;
<"@R.PREPREF.dernoun@" "@P.PREF.null@" ["@P.SUFF.an@" | "@P.SUFF.null@"]>                                    Numeral ;


<"@R.PREPREF.null@" "@P.PREF.null@">        Pronoun ; ! check
<"@R.PREPREF.null@" "@P.PREF.null@">        Subordinating ; ! check

LEXICON PREF-Ber
<0:ber "@R.PREPREF.null@" "@P.PREF.ber@" "@P.VOICE.av@" "@P.VERB.imper@">                                          Verb2 ;
<0:ber "@R.PREPREF.null@" "@P.PREF.ber@" "@P.VOICE.av@" "@P.VERBVAR.vbhaver@" "@P.NUM.sg@">                        Noun ;
<0:ber "@R.PREPREF.null@" "@P.PREF.ber@" "@P.VOICE.av@" "@P.VERBVAR.vbhaver@" "@P.NUM.sg@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" ]>        Noun ;
<0:ber "@R.PREPREF.null@" "@P.PREF.ber@" "@P.VOICE.av@" "@P.VERBVAR.vbhaver@" "@P.NUM.sg@">                        Adjective ;
<0:ber "@R.PREPREF.null@" "@P.PREF.ber@" "@P.VOICE.av@" "@P.VERBVAR.vbhaver@" "@P.NUM.sg@" "@P.NOUN.ent@" "@P.SUFF.an@">        Verb ;
<0:ber "@R.PREPREF.null@" "@P.PREF.ber@" "@P.VOICE.av@" "@P.VERBVAR.vbhaver@" "@P.NUM.sg@" "@P.SUFF.kan@">                      Noun ;
<0:ber "@R.PREPREF.null@" "@P.PREF.ber@" "@P.VOICE.av@" "@P.VERBVAR.vbhaver@" "@P.NUM.sg@" "@P.NOUN.ent@" "@P.SUFF.kan@">       Verb ;
<0:ber "@R.PREPREF.null@" "@P.PREF.ber@" "@P.NUM.coll@">       Numeral ;

LEXICON PREF-Per
<0:per "@R.PREPREF.null@" "@P.PREF.per@" "@P.VOICE.av@" "@P.VERBVAR.vbser@" "@P.ADJ.elev@" "@P.OBJ.caus@" "@D.SUFF@">       Adjective ;
<[0 .x. p e r] "@R.PREPREF.null@" "@P.PREF.per@" "@P.NOUN.theme@" "@P.NUM.sg@" "@R.SUFF.an@">       Noun ; 
<[0 .x. p e r] "@R.PREPREF.null@" "@P.PREF.per@" "@P.NOUN.theme@" "@P.NUM.sg@" "@R.SUFF.an@">       Verb ; 
<[0 .x. p e r] "@R.PREPREF.null@" "@P.PREF.per@" "@P.NOUN.theme@" "@P.NUM.sg@" "@R.SUFF.an@">       Adjective ; 
<[0 .x. p e r] "@R.PREPREF.null@" "@P.PREF.per@" "@P.NOUN.theme@" "@P.NUM.sg@" "@R.SUFF.an@">       Numeral ; 
<[0 .x. p e r] "@R.PREPREF.null@" "@P.PREF.per@" "@P.VOICE.av@" "@R.SUFF.kan@">       Noun ; 
<[0 .x. p e r] "@R.PREPREF.null@" "@P.PREF.per@" "@P.VOICE.av@" "@R.SUFF.kan@">       Verb ; 
<[0 .x. p e r] "@R.PREPREF.null@" "@P.PREF.per@" "@P.VOICE.av@" "@R.SUFF.kan@">       Adjective ; 
<[0 .x. p e r] "@R.PREPREF.null@" "@P.PREF.per@" "@P.VOICE.av@" "@R.SUFF.kan@">       Numeral ; 


LEXICON RedupOption
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@P.NUM.pl@">		Suffix ;
<"@D.REDUP.on@">        Suffix ;

LEXICON Suffix
<"@R.SUFF.an@">      SuffixAN ;
<"@R.SUFF.kan@">     SuffixKAN ;
<"@R.SUFF.i@">       SuffixI ;
<"@R.SUFF.null@">    ClosureCheck ;
<"@D.SUFF@">         ClosureCheck ;

LEXICON SuffixAN
<0:an "@R.PREPREF.null@" "@R.PREF.null@" "@P.NOUN.ent@" "@P.NUM.sg@" "@D.REDUP@">     ClosureCheck ;
<[0 .x. a n "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.PREPREF.null@" "@R.PREF.null@" "@R.REDUP.onsuff@" "@P.NOUN.ent@" "@P.NUM.pl@">		ClosureCheck ;
!<0:an "@R.PREPREF.null@" "@R.PREF.ber@" "@P.NOUN.ent@" "@P.NUM.sg@" "@D.REDUP@">     ClosureCheck ;
<[0 .x. a n "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.PREPREF.null@" "@R.PREF.per@" "@R.REDUP.onsuff@" "@P.NUM.pl@">		ClosureCheck ;
<0:an "@R.PREPREF.null@" "@R.PREF.per@" "@D.REDUP@">		ClosureCheck ;
<0:an "@R.PREPREF.dernoun@" "@R.PREF.null@" "@P.NUM.sg@" "@P.NOUN.theme@" "@D.REDUP.onsuff@">     ClosureCheck ;
<[0 .x. a n "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.PREPREF.dernoun@" "@R.PREF.null@" "@R.REDUP.onsuff@" "@P.NUM.pl@" "@P.NOUN.theme@">		ClosureCheck ;

LEXICON SuffixKAN
<0:kan "@R.PREPREF.null@" "@R.PREF.null@" "@P.OBJ.caus@">     ClosureCheck ;
<0:kan "@R.PREPREF.null@" "@R.PREF.ber@">      ClosureCheck ;
<0:kan "@R.PREPREF.null@" "@R.PREF.per@" "@P.OBJ.caus@">     ClosureCheck ;
<0:kan "@R.PREPREF.act@" "@R.PREF.null@" "@P.OBJ.caus@">     ClosureCheck ;
<0:kan "@R.PREPREF.pass@" "@R.PREF.null@" "@P.OBJ.caus@">     ClosureCheck ;

LEXICON SuffixI
<0:i "@R.PREPREF.null@" "@R.PREF.null@" "@P.OBJ.appl@">     ClosureCheck ;
<0:i "@R.PREPREF.null@" "@R.PREF.per@" "@P.OBJ.appl@">     ClosureCheck ;
<0:i "@R.PREPREF.act@" "@R.PREF.null@" "@P.OBJ.appl@">     ClosureCheck ;
<0:i "@R.PREPREF.pass@" "@R.PREF.null@" "@P.OBJ.appl@">     ClosureCheck ;



Lexicon ClosureCheck
<[%<v%> .x. 0] "@R.POS.v@">                        CheckVoice ;
<[%<n%> .x. 0] "@R.POS.n@">                        CheckVoice ;
<[%<adj%> .x. 0] "@R.POS.adj@">	                   CheckVoice ;
<[%<num%> .x. 0] "@R.POS.num@">		           CheckVoice ;
<[%<prn%> .x. 0] "@R.POS.prn@">		           CheckVoice ;
<[%<cnjsub%> .x. 0] "@R.POS.cnjsub@">              CheckVoice ;
<[%<cnjcoo%> .x. 0] "@R.POS.cnjcoo@">	           CheckVoice ;

Lexicon CheckVoice
<[%<av%> .x. 0] "@R.VOICE.av@">		           CheckDef ;
<[%<pv%> .x. 0] "@R.VOICE.pv@">                    CheckDef ;
<"@D.VOICE@">                                      CheckDef ;

Lexicon CheckDef
<[%<per%> .x. 0] "@R.VERB.per@">		   CheckNounType ;
<[%<imper%> .x. 0] "@R.VERB.imper@">		   CheckNounType ;
<"@D.VERB@">                                       CheckNounType ;

Lexicon CheckNounType
<[%<ent%> .x. 0] "@R.NOUN.ent@">		   CheckVerbVariance ;
<[%<theme%> .x. 0] "@R.NOUN.theme@">		   CheckVerbVariance ;
!<[%<ger%> .x. 0] "@R.NOUN.ger@">		   CheckVerbVariance ;
<[%<actor%> .x. 0] "@R.NOUN.act@">		   CheckVerbVariance ;
<"@D.NOUN@">                                       CheckVerbVariance ;

Lexicon CheckVerbVariance
<[%<vbser%> .x. 0] "@R.VERBVAR.vbser@">		   CheckObject ;
<[%<vbhaver%> .x. 0] "@R.VERBVAR.vbhaver@">        CheckObject ;
<"@D.VERBVAR@">                                    CheckObject ;

Lexicon CheckObject
<[%<caus%> .x. 0] "@R.OBJ.caus@">		   CheckAdj ;
<[%<appl%> .x. 0] "@R.OBJ.appl@">		   CheckAdj ;
<"@D.OBJ@">                                        CheckAdj ;

Lexicon CheckAdj
<[%<elev%> .x. 0] "@R.ADJ.elev@">                  CheckNum ;
<[%<sup%> .x. 0] "@R.ADJ.sup@">                    CheckNum ;
<"@D.ADJ@">                                        CheckNum ;


Lexicon CheckNum
<[%<sg%> .x. 0] "@R.NUM.sg@">           CheckCaps ;
<[%<pl%> .x. 0] "@R.NUM.pl@">           CheckCaps ;
<[%<coll%> .x. 0] "@R.NUM.coll@">       CheckCaps ;
<[%<ord%> .x. 0] "@R.NUM.ord@">         CheckCaps ;
<[%<car%> .x. 0] "@R.NUM.car@">         CheckCaps ;
<"@D.NUM@">                             CheckCaps ;

Lexicon CheckCaps
<[%<cap%> .x. 0] "@R.CAP.on@">           # ;
<"@D.CAP@">                              # ;





LEXICON Verb
< ["@P.CAP.on@" t:T | t] ulis "@P.POS.v@">        RedupOption ;
< ["@P.CAP.on@" k:K | k] irim "@P.POS.v@">        RedupOption ;
< ["@P.CAP.on@" d:D | d] ukung "@P.POS.v@">       RedupOption ;

LEXICON Verb2
< ["@P.CAP.on@" l:L | l] ari "@P.POS.v@">         RedupOption ;

LEXICON Noun
< ["@P.CAP.on@" p:P | p] esan "@P.POS.n@">        RedupOption ;
< ["@P.CAP.on@" p:P | p] es "@P.POS.n@">          RedupOption ; !toy
< ["@P.CAP.on@" p:P | p] erangkat "@P.POS.n@">    RedupOption ;
< ["@P.CAP.on@" t:T | t] eks "@P.POS.n@">         RedupOption ;
< ["@P.CAP.on@" b:B | b] atas "@P.POS.n@">        RedupOption ;
< ["@P.CAP.on@" k:K | k] arakter "@P.POS.n@">     RedupOption ;
< ["@P.CAP.on@" t:T | t] oko "@P.POS.n@">         RedupOption ;

LEXICON Adjective
< ["@P.CAP.on@" b:B | b] esar "@P.POS.adj@">      RedupOption ;
< ["@P.CAP.on@" l:L | l] ebih "@P.POS.adj@">      RedupOption ;

LEXICON Numeral
< ["@P.CAP.on@" d:D | d] ua "@P.POS.num@">        RedupOption ;
< ["@P.CAP.on@" t:T | t] iga "@P.POS.num@">       RedupOption ;


LEXICON Pronoun
< ["@P.CAP.on@" a:A | a] nda "@P.POS.prn@">       RedupOption ;

LEXICON Subordinating
< ["@P.CAP.on@" y:Y | y] ang "@P.POS.cnjsub@">    RedupOption ;

