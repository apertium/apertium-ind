!!! ================================================================
!!! Indonesian Morphological Analyzer
!!! Modified by : Septina Dian Larasati (septina.larasati@gmail.com)
!!! Date        : 18 June 2010
!!! ================================================================


!!! -- Multicharacter Definitions --
!!! Includes the Morphological Tags, Nasal Marks, and Diacritic Flags
!!! <TAGS>, ^NASAL, and @THE.DIACRITIC.FLAG@
!!! ----------------------------------------------------------------
Multichar_Symbols
         %<abstract%>           !! Morphological Tags
         %<act%>
         %<actor%>
         %<adj%>
         %<appl%>
         %<av%>
         %<bare%>
         %<cap%>
         %<car%>
         %<caus%>
         %<cnjcoo%>
         %<cnjsub%>
         %<coll%>
         %<det%>
         %<enc%>
         %<ent%>
         %<exceed%>
         %<imp%>
         %<manner%>
         %<n%>
         %<num%>
         %<ord%>
         %<p1%>
         %<p2%>
         %<p3%>
         %<perf%>
         %<pl%>
         %<pos%>
         %<positive%>
         %<prn%>
         %<pro%>
         %<pv%>
         %<ref%>
         %<sg%>
         %<sup%>
         %<theme%>
         %<uni%>
         %<v%>
         %<vbhaver%>
         %<vbser%>
         %<par%>
         %<uni%>
         %<mod%>
         %<inj%>
         %<qst%>
         %<par%>
         %<pre%>
         %<possib%>
	 ^N                        !! Nasal Mark
	 ^REHYPH
	 ^[
	 ^2^]
	 +
	 -
         @P.ASPECT.imperf@         !! Diacritic Flags
         @P.ASPECT.perf@
         @P.CAP.on@
         @P.DERADJ.exceed@
         @P.DERADJ.manner@
         @P.DERADJ.positive@
         @P.DERADJ.sup@
         @P.DERADJ.uni@
         @P.DERADJ.possib@
         @P.DERNOUN.abstract@
         @P.DERNOUN.act@
         @P.DERNOUN.actor@
         @P.DERNOUN.bare@
         @P.DERNOUN.ent@
         @P.DERNOUN.theme@
         @P.DERNUM.car@
         @P.DERNUM.coll@
         @P.DERNUM.ord@
         @P.DERNUM.ref@
         @P.DERNUM.uni@
         @P.ENCLITIC.enc@
         @P.FLAG.bare@
         @P.FLAG.deradj@
         @P.FLAG.dernoun@
         @P.FLAG.numinfl@
         @P.FLAG.verbinfl@
         @P.MARK.bare@
         @P.MARK.ber@
         @P.MARK.ke@
         @P.MARK.on@
         @P.MARK.pen@
         @P.MARK.per@
         @P.MARK.ter@
         @P.MARK.se@
         @P.NUM.pl@
         @P.NUM.sg@
         @P.OBJ.appl@
         @P.OBJ.caus@
         @P.PERSON.p1@
         @P.PERSON.p2@
         @P.PERSON.p3@
         @P.POS.adj@
         @P.POS.adv@
         @P.POS.cnjcoo@
         @P.POS.cnjsub@
         @P.POS.det@
         @P.POS.mod@
         @P.POS.interjection@
         @P.POS.n@
         @P.POS.num@
         @P.POS.prn@
         @P.POS.question@
         @P.POS.v@
         @P.PROCLITIC.pro@
         @P.REDUP.on@
         @P.VERBVAR.vbhaver@
         @P.VERBVAR.vbser@
         @P.VOICE.av@
         @P.VOICE.pv@
         @R.ASPECT.imperf@
         @R.ASPECT.perf@
         @R.CAP.on@
         @R.DERADJ.exceed@
         @R.DERADJ.manner@
         @R.DERADJ.positive@
         @R.DERADJ.sup@
         @R.DERADJ.uni@
         @R.DERADJ.possib@
         @R.DERNOUN.abstract@
         @R.DERNOUN.act@
         @R.DERNOUN.actor@
         @R.DERNOUN.bare@
         @R.DERNOUN.ent@
         @R.DERNOUN.theme@
         @R.DERNUM.car@
         @R.DERNUM.coll@
         @R.DERNUM.ord@
         @R.DERNUM.ref@
         @R.DERNUM.uni@
         @R.ENCLITIC.enc@
         @R.FLAG.bare@
         @R.FLAG.deradj@
         @R.FLAG.dernoun@
         @R.FLAG.numinfl@
         @R.FLAG.verbinfl@
         @R.MARK.bare@
         @R.MARK.ber@
         @R.MARK.ke@
         @R.MARK.on@
         @R.MARK.pen@
         @R.MARK.per@
         @R.MARK.ter@
         @R.MARK.se@
         @R.NUM.pl@
         @R.NUM.sg@
         @R.OBJ.appl@
         @R.OBJ.caus@
         @R.PERSON.p1@
         @R.PERSON.p2@
         @R.PERSON.p3@
         @R.POS.adj@
         @R.POS.adv@
         @R.POS.cnjcoo@
         @R.POS.cnjsub@
         @R.POS.det@
         @R.POS.mod@
         @R.POS.interjection@
         @R.POS.n@
         @R.POS.num@
         @R.POS.prn@
         @R.POS.question@
         @R.POS.v@
         @R.PROCLITIC.pro@
         @R.REDUP.on@
         @R.VERBVAR.vbhaver@
         @R.VERBVAR.vbser@
         @R.VOICE.av@
         @R.VOICE.pv@
         @R.ASPECT@
         @R.CAP@
         @R.DERADJ@
         @R.DERNOUN@
         @R.DERNUM@
         @R.ENCLITIC@
         @R.FLAG@
         @R.MARK@
         @R.NUM@
         @R.OBJ@
         @R.PERSON@
         @R.POS@
         @R.PROCLITIC@
         @R.REDUP@
         @R.VERBVAR@
         @R.VOICE@
         @D.ASPECT@
         @D.CAP@
         @D.DERADJ@
         @D.DERNOUN@
         @D.DERNUM@
         @D.ENCLITIC@
         @D.FLAG@
         @D.MARK@
         @D.NUM@
         @D.OBJ@
         @D.PERSON@
         @D.POS@
         @D.PROCLITIC@
         @D.REDUP@
         @D.VERBVAR@
         @D.VOICE@
         @D.MARK.bare@



!!! -- Morphotactic Rules --
!!! 
!!! 
!!! ----------------------------------------------------------------

!! ROOT node
!! Includes the Pronoun Proclitics
LEXICON Root
aku%<prn%>%<pro%>%<p1%>%<sg%>%+@P.FLAG.verbinfl@:ku000000@P.FLAG.verbinfl@                   VerbInflection ;
aku%<prn%>%<pro%>%<p1%>%<sg%>%<cap%>%+@P.FLAG.verbinfl@:Ku0000000@P.FLAG.verbinfl@           VerbInflection ;
kamu%<prn%>%<pro%>%<p2%>%<sg%>%+@P.FLAG.verbinfl@:kau000000@P.FLAG.verbinfl@                 VerbInflection ;
kamu%<prn%>%<pro%>%<p2%>%<sg%>%<cap%>%+@P.FLAG.verbinfl@:Kau0000000@P.FLAG.verbinfl@         VerbInflection ;
@P.CAP.on@                                                                                   MainWord ;
                                                                                             MainWord ;
!! MAIN WORD branch
!! Includes:
!! - Verb and Numeral Inflection
!! - Adjective and Noun Derivation
!! - Others
LEXICON MainWord
@P.FLAG.numinfl@                                   NumeralInflection ;
@P.FLAG.deradj@                                    DerivingAdjective ;
@P.FLAG.dernoun@                                   DerivingNoun-Singular ;
@P.FLAG.dernoun@                                   DerivingNoun-Plural ;
@P.FLAG.verbinfl@                                  VerbInflection ;
                                                   Pronoun ;
                                                   Subordinating ;
                                                   Coordinating ;
                                                   Determiner ;
                                                   Adverb ;
                                                   Question ;
                                                   Modal ;
                                                   Interjection ;
                                                   Particle ;
                                                   Preposition ;
                                                   Exception ;



!! -- Section: PREFIX

!! Numeral Inflections
LEXICON NumeralInflection
@P.MARK.bare@                                                               Numeral ;  ! eg. "tujuh" [seven]
00@P.MARK.ke@:ke@P.MARK.ke@                                                 Numeral ;  ! eg. "ke- + tujuh" [seventh]
00@P.MARK.ke@@P.REDUP.on@000:ke@P.MARK.ke@@P.REDUP.on@%^%[%[%{              Numeral ;  ! eg. "ke- + tujuh-tujuh + -nya" [all the seven of them]
@P.MARK.bare@@P.REDUP.on@000:@P.MARK.bare@@P.REDUP.on@%^%[%[%{              Numeral ;  ! eg. "tujuh-tujuh + -nya" [all the seven of them]
000@P.MARK.ber@:ber@P.MARK.ber@                                             Numeral ;  ! eg. "ber- + tujuh" [in group of seven]
satu%<num%>%<car%>+per%<adv%>+@P.MARK.bare@:seper0000000@P.MARK.bare@       Numeral ;  ! eg. "se- + per + tujuh" [one seventh, 1/7, lit. one each seven]

!! Adjective Derivations
LEXICON DerivingAdjective
@P.MARK.bare@                                                               Adjective ;   ! eg. "cantik" [beautiful]
@P.MARK.bare@@P.REDUP.on@000:@P.MARK.bare@@P.REDUP.on@%^%[%[%{              Adjective ;   ! eg. "cantik-cantik" [beautiful (addressing to plural references)]
00@P.MARK.ke@:ke@P.MARK.ke@                                                 Noun ;        ! eg. "ke- + ibu + -an" [motherly]
00@P.MARK.ke@@P.REDUP.on@000:ke@P.MARK.ke@@P.REDUP.on@%^%[%[%{              Noun ;        ! eg. "ke- + ibu-ibu + -an" [like mothers]
00@P.MARK.ke@:ke@P.MARK.ke@                                                 Adjective ;   ! eg. "ke- + cantik + an" [too beautiful (equally grammatical with "terlalu cantik")]
000@P.MARK.ter@:ter@P.MARK.ter@                                             Adjective ;   ! eg. "ter- + cantik" [the most beautiful (equally grammatical with "paling cantik")]
00@P.MARK.se@@P.FLAG.dernoun@@P.DERADJ.uni@:se@P.MARK.se@@P.FLAG.dernoun@@P.DERADJ.uni@        DerivingNoun-Singular ;      ! eg. "se- + ibu" [one/same mother]
00@P.MARK.se@@P.DERADJ.uni@:se@P.MARK.se@@P.DERADJ.uni@                                        Adjective ;                  ! eg. "se- + cantik" [as beautiful as]
00@P.MARK.se@@P.DERADJ.uni@@P.REDUP.on@000:se@P.MARK.se@@P.DERADJ.uni@@P.REDUP.on@%^%[%[%{     Adjective ;                  ! eg. "se- + besar-besar + -nya" [as big as something can possibly be]

!! Noun Derivations
!! Devided into Singular and Plural just because of the implementation issues
!! The plurality of the Nouns are marked by reduplication
LEXICON DerivingNoun-Singular
@P.NUM.sg@@P.MARK.bare@                                                                         Verb ;        ! eg. "kirim + -an" [package]
@P.NUM.sg@@P.MARK.bare@                                                                         Noun ;        ! eg. "sapu + -an" [stroke (of a sweep/brush, sapu = sweep)]
@P.NUM.sg@@P.MARK.bare@                                                                         Adjective ;   ! eg. "besar + -an" [metric]
@P.NUM.sg@@P.MARK.bare@                                                                         Numeral ;     ! eg. "ratus + -an" [hundreds] xxx?
@P.NUM.sg@@P.MARK.pen@@P.DERNOUN.actor@000:@P.NUM.sg@@P.MARK.pen@@P.DERNOUN.actor@pe%^N         Verb ;        ! eg. "peN- + kirim" [deliverer], "peN- + kirim + -an" [the process of delivering / delivery]
@P.NUM.sg@@P.MARK.pen@@P.DERNOUN.actor@000:@P.NUM.sg@@P.MARK.pen@@P.DERNOUN.actor@pe%^N         Noun ;        ! eg. "peN- + sapu" [sweeper], "peN- + sapu + -an" [the act of stroking]
@P.NUM.sg@@P.MARK.pen@@P.DERNOUN.actor@000:@P.NUM.sg@@P.MARK.pen@@P.DERNOUN.actor@pe%^N         Adjective ;   ! eg. "peN- + dingin" [cooler], "peN- + dingin + -an" [the process of cooling]
@P.NUM.sg@@P.MARK.pen@@P.DERNOUN.actor@000:@P.NUM.sg@@P.MARK.pen@@P.DERNOUN.actor@pe%^N         Numeral ;     ! eg. xxx?
@P.NUM.sg@00@P.MARK.ke@:@P.NUM.sg@ke@P.MARK.ke@                                                 Adjective ;   ! eg. "ke- + dingin + -an" [coolness]
@P.NUM.sg@00@P.MARK.ke@:@P.NUM.sg@ke@P.MARK.ke@                                                 Verb ;        ! eg. xxx?
@P.NUM.sg@000@P.MARK.per@:@P.NUM.sg@per@P.MARK.per@                                             Noun ;        ! eg. "per- + toko + -an" [shoping centre]
@P.NUM.sg@000@P.MARK.per@:@P.NUM.sg@per@P.MARK.per@                                             Adjective ;   ! eg. "per- + cepat + -an" [velocity]
@P.NUM.sg@000@P.MARK.per@:@P.NUM.sg@per@P.MARK.per@                                             Numeral ;     ! eg. "per- + tiga + -an" [three-way road junction]

!! Examples see above and reduplicate them
LEXICON DerivingNoun-Plural
@P.MARK.bare@@P.REDUP.on@000:@P.MARK.bare@@P.REDUP.on@%^%[%[%{                                                Verb ;
@P.MARK.bare@@P.REDUP.on@000:@P.MARK.bare@@P.REDUP.on@%^%[%[%{                                                Noun ;
@P.MARK.bare@@P.REDUP.on@000:@P.MARK.bare@@P.REDUP.on@%^%[%[%{                                                Adjective ;
@P.MARK.bare@@P.REDUP.on@000:@P.MARK.bare@@P.REDUP.on@%^%[%[%{                                                Numeral ;
@P.MARK.pen@@P.DERNOUN.actor@@P.REDUP.on@000000:@P.MARK.pen@@P.DERNOUN.actor@@P.REDUP.on@%^%[%[%{pe%^N        Verb ;
@P.MARK.pen@@P.DERNOUN.actor@@P.REDUP.on@000000:@P.MARK.pen@@P.DERNOUN.actor@@P.REDUP.on@%^%[%[%{pe%^N        Noun ;
@P.MARK.pen@@P.DERNOUN.actor@@P.REDUP.on@000000:@P.MARK.pen@@P.DERNOUN.actor@@P.REDUP.on@%^%[%[%{pe%^N        Adjective ;
@P.MARK.pen@@P.DERNOUN.actor@@P.REDUP.on@000000:@P.MARK.pen@@P.DERNOUN.actor@@P.REDUP.on@%^%[%[%{pe%^N        Numeral ;
@P.REDUP.on@@P.MARK.ke@00000:@P.REDUP.on@@P.MARK.ke@%^%[%[%{ke                                                Adjective ;
@P.REDUP.on@@P.MARK.ke@00000:@P.REDUP.on@@P.MARK.ke@%^%[%[%{ke                                                Verb ;
@P.REDUP.on@@P.MARK.per@000000:@P.REDUP.on@@P.MARK.per@%^%[%[%{per                                            Noun ;
@P.REDUP.on@@P.MARK.per@000000:@P.REDUP.on@@P.MARK.per@%^%[%[%{per                                            Adjective ;
@P.REDUP.on@@P.MARK.per@000000:@P.REDUP.on@@P.MARK.per@%^%[%[%{per                                            Numeral ;

!! Verb Inflections
LEXICON VerbInflection
@P.VOICE.av@@P.ASPECT.perf@@P.NUM.sg@                                                                         Verb ;       ! eg. "kirim" [send, <av><perf>]
@P.VOICE.av@@P.ASPECT.perf@@P.REDUP.on@000:@P.VOICE.av@@P.ASPECT.perf@@P.REDUP.on@%^%[%[%{                    Verb ;       ! eg. "kirim-kirim" [send in plural sense, <av><perf>]
@P.VOICE.av@@P.ASPECT.imperf@@P.NUM.sg@000:@P.VOICE.av@@P.ASPECT.imperf@@P.NUM.sg@me%^N                       Verb ;       ! eg. "meN- + kirim" [send, <av><imp>]
@P.VOICE.av@@P.ASPECT.imperf@@P.REDUP.on@000000:@P.VOICE.av@@P.ASPECT.imperf@@P.REDUP.on@me%^N%^%[%[%{        Verb ;       ! eg. "meN- + kirim-kirim" [send in plural sense, <av><imp>]
@P.VOICE.av@@P.ASPECT.imperf@@P.NUM.sg@000:@P.VOICE.av@@P.ASPECT.imperf@@P.NUM.sg@me%^N                       Noun ;       ! eg. "meN- + patung" [becoming statue, <av><imp>] xxx? check!
@P.VOICE.av@@P.ASPECT.imperf@@P.REDUP.on@000000:@P.VOICE.av@@P.ASPECT.imperf@@P.REDUP.on@me%^N%^%[%[%{        Noun ;       ! eg. "meN- + patung-patung" [becoming statue in plural sense, <av><imp>]
@P.VOICE.av@@P.ASPECT.imperf@@P.NUM.sg@000:@P.VOICE.av@@P.ASPECT.imperf@@P.NUM.sg@me%^N                       Adjective ;  ! eg. "meN- + dingin" [becoming cold, <av><imp>]
@P.VOICE.av@@P.ASPECT.imperf@@P.REDUP.on@000000:@P.VOICE.av@@P.ASPECT.imperf@@P.REDUP.on@me%^N%^%[%[%{        Adjective ;  ! eg. "meN- + dingin-dingin" [becoming cold in plural sense, <av><imp>]
@P.VOICE.av@@P.ASPECT.imperf@@P.NUM.sg@000:@P.VOICE.av@@P.ASPECT.imperf@@P.NUM.sg@me%^N                       Numeral ;    ! eg. "meN- + dua" [becoming two, <av><imp>] xxx? check!
@P.VOICE.av@@P.ASPECT.imperf@@P.REDUP.on@000000:@P.VOICE.av@@P.ASPECT.imperf@@P.REDUP.on@me%^N%^%[%[%{        Numeral ;    ! eg. "meN- + dua-dua" [becoming two in plural sense, <av><imp>] xxx? check!
@P.VOICE.pv@@P.ASPECT.imperf@@P.NUM.sg@00:@P.VOICE.pv@@P.ASPECT.imperf@@P.NUM.sg@di                           Verb ;       ! eg. "di- + kirim" [be send, <pv><imp>]
@P.VOICE.pv@@P.ASPECT.imperf@@P.REDUP.on@00000:@P.VOICE.pv@@P.ASPECT.imperf@@P.REDUP.on@di%^%[%[%{            Verb ;       ! eg. "di- + kirim-kirim" [be send in plural sense, <pv><imp>]
@P.VOICE.pv@@P.ASPECT.imperf@@P.NUM.sg@00:@P.VOICE.pv@@P.ASPECT.imperf@@P.NUM.sg@di                           Noun ;       ! eg. "di- + patung + -kan" [be statued, <pv><imp>]
@P.VOICE.pv@@P.ASPECT.imperf@@P.REDUP.on@00000:@P.VOICE.pv@@P.ASPECT.imperf@@P.REDUP.on@di%^%[%[%{            Noun ;       ! eg. "di- + patung + -kan" [be statued in plural sense, <pv><imp>]
@P.VOICE.pv@@P.ASPECT.imperf@@P.NUM.sg@00:@P.VOICE.pv@@P.ASPECT.imperf@@P.NUM.sg@di                           Adjective ;  ! eg. "di- + dingin + -kan" [be colded, <pv><imp>]
@P.VOICE.pv@@P.ASPECT.imperf@@P.REDUP.on@00000:@P.VOICE.pv@@P.ASPECT.imperf@@P.REDUP.on@di%^%[%[%{            Adjective ;  ! eg. "di- + dingin-dingin + -kan" [be colded in plural sense, <pv><imp>]
@P.VOICE.pv@@P.ASPECT.imperf@@P.NUM.sg@00:@P.VOICE.pv@@P.ASPECT.imperf@@P.NUM.sg@di                           Numeral ;    ! eg. "di- + dua + -kan" [made into two, <pv><imp>] xxx? check?
@P.VOICE.pv@@P.ASPECT.imperf@@P.REDUP.on@00000:@P.VOICE.pv@@P.ASPECT.imperf@@P.REDUP.on@di%^%[%[%{            Numeral ;    ! eg. "di- + dua-dua + -kan" [made into two in plural sense, <pv><imp>] xxx? check?
@P.VOICE.pv@@P.ASPECT.perf@@P.NUM.sg@000:@P.VOICE.pv@@P.ASPECT.perf@@P.NUM.sg@ter                             Verb ;       ! eg. "ter- + kirim" [be send, <pv><perf>]
@P.VOICE.pv@@P.ASPECT.perf@@P.REDUP.on@000000:@P.VOICE.pv@@P.ASPECT.perf@@P.REDUP.on@ter%^%[%[%{              Verb ;       ! eg. "ter- + kirim-kirim" [be send in plural sense, <pv><perf>]
@P.VOICE.pv@@P.ASPECT.perf@@P.NUM.sg@000:@P.VOICE.pv@@P.ASPECT.perf@@P.NUM.sg@ter                             Noun ;       ! eg. "ter- + patung" [becoming statue, <pv><perf>]
@P.VOICE.pv@@P.ASPECT.perf@@P.REDUP.on@000000:@P.VOICE.pv@@P.ASPECT.perf@@P.REDUP.on@ter%^%[%[%{              Noun ;       ! eg. "ter- + patung-patung" [becoming statue in plural sense, <pv><perf>]
@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbser@@P.NUM.sg@000:@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbser@@P.NUM.sg@per                                          Adjective ;       ! eg. "per- + besar" [make to become bigger, <av><perf>]
@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbser@@P.REDUP.on@000000:@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbser@@P.REDUP.on@per%^%[%[%{                           Adjective ;       ! eg. "per- + besar-besar" [make to become bigger in plural sense, <av><perf>]
@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@000000:@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@memper                                Adjective ;       ! eg. "meN- + per- + besar" [make to become bigger, <av><imp>]
@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@000000000:@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@memper%^%[%[%{                 Adjective ;       ! eg. "meN- + per- + besar-besar" [make to become bigger in plural sense, <av><imp>]
@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@000000:@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@memper                                Verb ;            ! eg. xxx check?
@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@000000000:@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@memper%^%[%[%{                 Verb ;            ! eg. xxx check?
@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@000000:@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@memper                                Noun ;            ! eg. xxx check?
@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@000000000:@P.VOICE.av@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@memper%^%[%[%{                 Noun ;            ! eg. xxx check?
@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@00000:@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@diper                                  Adjective ;       ! eg. "di + per- + besar" [make to become bigger, <pv><imp>]
@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@00000000:@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@diper%^%[%[%{                   Adjective ;       ! eg. "di + per- + besar-besar" [make to become bigger in plural sense, <pv><imp>]
@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@00000:@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@diper                                  Verb ;            ! eg. xxx check?
@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@00000000:@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@diper%^%[%[%{                   Verb ;            ! eg. xxx check?
@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@00000:@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.NUM.sg@diper                                  Noun ;            ! eg. xxx check?
@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@00000000:@P.VOICE.pv@@P.ASPECT.imperf@@P.VERBVAR.vbser@@P.REDUP.on@diper%^%[%[%{                   Noun ;            ! eg. xxx check?
@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbhaver@@P.NUM.sg@@P.FLAG.dernoun@000:@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbhaver@@P.NUM.sg@@P.FLAG.dernoun@ber      DerivingNoun-Singular ;            ! eg. "ber- + topi" [having hat]
@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbhaver@@P.NUM.sg@@P.FLAG.dernoun@000:@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbhaver@@P.NUM.sg@@P.FLAG.dernoun@ber      DerivingNoun-Plural ;              ! eg. "ber- + topi" [having hats]
@P.VOICE.av@@P.ASPECT.perf@@P.NUM.sg@000:@P.VOICE.av@@P.ASPECT.perf@@P.NUM.sg@ber                                                                            Verb  ;                            ! eg. "ber- + lari" [run, <av><perf>]
@P.VOICE.av@@P.ASPECT.perf@@P.NUM.sg@000:@P.VOICE.av@@P.ASPECT.perf@@P.NUM.sg@ber                                                                            Adjective ;                        ! eg. "ber- + besar" [having something big, <av><perf>]
@P.VOICE.av@@P.ASPECT.perf@@P.REDUP.on@000000:@P.VOICE.av@@P.ASPECT.perf@@P.REDUP.on@ber%^%[%[%{                                                             Verb  ;                            ! eg. "ber- + lari" [run in plural sense, <av><perf>]
@P.VOICE.av@@P.ASPECT.perf@@P.REDUP.on@000000:@P.VOICE.av@@P.ASPECT.perf@@P.REDUP.on@ber%^%[%[%{                                                             Adjective ;                        ! eg. "ber- + besar" [having something big in plural sense, <av><perf>]
@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbser@@P.NUM.sg@@P.MARK.bare@                                                                                          Adjective ;                        ! eg. "besar + kan" [make to become big, <av><perf>]
@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbser@@P.MARK.bare@@P.REDUP.on@000:@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbser@@P.MARK.bare@@P.REDUP.on@%^%[%[%{       Adjective ;                        ! eg. "besar-besar + kan" [make to become big in plural sense, <av><perf>]
@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbser@@P.NUM.sg@@P.MARK.bare@                                                                                          Noun ;                             ! eg. "buku + kan" [make to become book, <av><perf>]
@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbser@@P.MARK.bare@@P.REDUP.on@000:@P.VOICE.av@@P.ASPECT.perf@@P.VERBVAR.vbser@@P.MARK.bare@@P.REDUP.on@%^%[%[%{       Noun ;                             ! eg. "buku-buku + kan" [make to become book in plural sense, <av><perf>]



!! -- Section: PREFIX

LEXICON InflectionOption
@R.FLAG.numinfl@            NumeralInflectionEnding ;
@R.FLAG.deradj@             DerivingAdjectiveEnding ;
@R.FLAG.dernoun@            DerivingNounEnding ;
@R.FLAG.verbinfl@           VerbInflectionEnding ;
@D.FLAG@                    Tagging ;

LEXICON NumeralInflectionEnding
@D.REDUP@@R.MARK.bare@@P.DERNUM.car@               Tagging ;
@D.REDUP@@R.MARK.ke@@P.DERNUM.ord@                 Tagging ;
000@D.REDUP@@R.MARK.ke@@P.DERNUM.ref@:nya@D.REDUP@@R.MARK.ke@@P.DERNUM.ref@                                      Tagging ;
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@C.REDUP@" "@R.MARK.ke@" "@P.DERNUM.ref@" 0:nya>        Tagging ;   !! check this later
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@C.REDUP@" "@R.MARK.bare@" "@P.DERNUM.ref@" 0:nya>        Tagging ;   !! check this later
@D.REDUP@@R.MARK.ber@@P.DERNUM.coll@               Tagging ;
belas@D.REDUP@@R.MARK.bare@@P.DERNUM.car@          Tagging ;

LEXICON DerivingAdjectiveEnding
00@D.REDUP@@R.MARK.ke@@R.POS.n@@P.DERADJ.manner@:an@D.REDUP@@R.MARK.ke@@R.POS.n@@P.DERADJ.manner@             Tagging ;
00@D.REDUP@@R.MARK.ke@@R.POS.adj@@P.DERADJ.exceed@:an@D.REDUP@@R.MARK.ke@@R.POS.adj@@P.DERADJ.exceed@         Tagging ;
@D.REDUP@@R.MARK.ter@@R.POS.adj@@P.DERADJ.sup@                                                                Tagging ;
@D.REDUP@@R.MARK.bare@@R.POS.adj@@P.DERADJ.positive@                                                          Tagging ;
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@C.REDUP@" "@P.NUM.pl@""@R.MARK.bare@" "@R.POS.adj@" "@P.DERADJ.positive@">      Tagging ;  !! check this later
@D.REDUP@@R.MARK.se@@R.POS.adj@:@D.REDUP@@R.MARK.se@@R.POS.adj@                                               Tagging ;
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@C.REDUP@" "@P.NUM.pl@""@R.MARK.ke@" "@R.POS.n@" "@P.DERADJ.manner@" 0:an>       Tagging ;  !! check this later
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@C.REDUP@" "@R.MARK.se@" "@R.POS.adj@" "@P.DERADJ.possib@" 0:nya>    Tagging ;  !! check this later

LEXICON DerivingNounEnding
@R.MARK.bare@@R.POS.n@@P.DERNOUN.bare@                                                   LastReduplicationCheck ;
00@R.MARK.bare@@P.DERNOUN.ent@:an@R.MARK.bare@@P.DERNOUN.ent@                            LastReduplicationCheck ;
00@R.MARK.pen@@P.DERNOUN.act@:an@R.MARK.pen@@P.DERNOUN.act@                              LastReduplicationCheck ;
@R.MARK.pen@                                                                             LastReduplicationCheck ;
00@R.MARK.ke@@P.DERNOUN.abstract@:an@R.MARK.ke@@P.DERNOUN.abstract@                      LastReduplicationCheck ;
00@R.MARK.per@@P.DERNOUN.theme@:an@R.MARK.per@@P.DERNOUN.theme@                          LastReduplicationCheck ;
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] 0:an "@R.REDUP.on@" "@C.REDUP@" "@P.NUM.pl@" "@R.MARK.bare@">        Tagging ;

LEXICON VerbInflectionEnding
@D.REDUP@                                                                                VerbInflectionEnding-main ;
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@C.REDUP@" "@P.NUM.pl@">        VerbInflectionEnding-main ;

LEXICON VerbInflectionEnding-main
000@P.OBJ.caus@:kan@P.OBJ.caus@            Tagging ;
0@P.OBJ.appl@:i@P.OBJ.appl@                Tagging ;
@D.MARK@                              Tagging ;

LEXICON LastReduplicationCheck
@D.REDUP@        Tagging ;
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@C.REDUP@" "@P.NUM.pl@">        LastReduplicationCheck ;


!! -- Section: TAGGING
!! but aslo includes pronoun proclitic and particle

LEXICON Tagging
%<v%>@R.POS.v@:0@R.POS.v@                                     CheckVoice ;
%<n%>@R.POS.n@:0@R.POS.n@                                     CheckVoice ;
%<adj%>@R.POS.adj@:0@R.POS.adj@                               CheckVoice ;
%<adv%>@R.POS.adv@:0@R.POS.adv@                               CheckVoice ;
%<num%>@R.POS.num@:0@R.POS.num@                               CheckVoice ;
%<prn%>@R.POS.prn@:0@R.POS.prn@                               CheckVoice ;
%<cnjsub%>@R.POS.cnjsub@:0@R.POS.cnjsub@                      CheckVoice ;
%<cnjcoo%>@R.POS.cnjcoo@:0@R.POS.cnjcoo@                      CheckVoice ;
%<det%>0@R.POS.det@:0@R.POS.det@                              CheckVoice ;
%<mod%>0@R.POS.mod@:0@R.POS.mod@                              CheckVoice ;
%<inj%>0@R.POS.interjection@:0@R.POS.interjection@            CheckVoice ;
%<qst%>0@R.POS.question@:0@R.POS.question@                    CheckVoice ;

LEXICON CheckVoice
%<av%>@R.VOICE.av@:0@R.VOICE.av@                              CheckAspect ;
%<pv%>@R.VOICE.pv@:0@R.VOICE.pv@                              CheckAspect ;
@D.VOICE@                                                     CheckAspect ;

LEXICON CheckAspect
%<perf%>@R.ASPECT.perf@:0@R.ASPECT.perf@                      CheckVerbVariance ;
%<imp%>@R.ASPECT.imperf@:0@R.ASPECT.imperf@                   CheckVerbVariance ;
@D.ASPECT@                                                    CheckVerbVariance ;

LEXICON CheckVerbVariance
%<vbser%>@R.VERBVAR.vbser@:0@R.VERBVAR.vbser@                 CheckTransitivity ;
%<vbhaver%>@R.VERBVAR.vbhaver@:0@R.VERBVAR.vbhaver@           CheckTransitivity ;
@D.VERBVAR@                                                   CheckTransitivity ;

LEXICON CheckTransitivity
%<caus%>@R.OBJ.caus@:0@R.OBJ.caus@                            CheckDerNoun ;
%<appl%>@R.OBJ.appl@:0@R.OBJ.appl@                            CheckDerNoun ;
@D.OBJ@                                                       CheckDerNoun ;

LEXICON CheckDerNoun
%<ent%>@R.DERNOUN.ent@:0@R.DERNOUN.ent@                       CheckDerNum ;
%<act%>@R.DERNOUN.act@:0@R.DERNOUN.act@                       CheckDerNum ;
%<actor%>@R.DERNOUN.actor@:0@R.DERNOUN.actor@                 CheckDerNum ;
%<theme%>@R.DERNOUN.theme@:0@R.DERNOUN.theme@                 CheckDerNum ;
%<abstract%>@R.DERNOUN.abstract@:0@R.DERNOUN.abstract@        CheckDerNum ;
%<bare%>@R.DERNOUN.bare@:0@R.DERNOUN.bare@                    CheckDerNum ;
@D.DERNOUN@                                                   CheckDerNum ;

LEXICON CheckDerNum
%<car%>@R.DERNUM.car@:0@R.DERNUM.car@                         CheckPerson ;
%<ord%>@R.DERNUM.ord@:0@R.DERNUM.ord@                         CheckPerson ;
%<coll%>@R.DERNUM.coll@:0@R.DERNUM.coll@                      CheckPerson ;
%<ref%>@R.DERNUM.ref@:0@R.DERNUM.ref@                         CheckPerson ;
%<uni%>@R.DERNUM.uni@:0@R.DERNUM.uni@                         CheckPerson ;
@D.DERNUM@                                                    CheckPerson ;

LEXICON CheckPerson
%<p1%>@R.PERSON.p1@:0@R.PERSON.p1@                            CheckDerAdj ;
%<p2%>@R.PERSON.p2@:0@R.PERSON.p2@                            CheckDerAdj ;
%<p3%>@R.PERSON.p3@:0@R.PERSON.p3@                            CheckDerAdj ;
@D.PERSON@                                                    CheckDerAdj ;

LEXICON CheckDerAdj
%<positive%>@R.DERADJ.positive@:0@R.DERADJ.positive@          CheckNum ;
%<sup%>@R.DERADJ.sup@:0@R.DERADJ.sup@                         CheckNum ;
%<manner%>@R.DERADJ.manner@:0@R.DERADJ.manner@                CheckNum ;
%<exceed%>@R.DERADJ.exceed@:0@R.DERADJ.exceed@                CheckNum ;
%<uni%>@R.DERADJ.uni@:0@R.DERADJ.uni@                         CheckNum ;
%<possib%>@R.DERADJ.possib@:0@R.DERADJ.possib@                CheckNum ;
@D.DERADJ@                                                    CheckNum ;

LEXICON CheckNum
%<sg%>@R.NUM.sg@:0@R.NUM.sg@                                  CheckCaps ;
%<pl%>@R.NUM.pl@:0@R.NUM.pl@                                  CheckCaps ;
@D.NUM@                                                       CheckCaps ;

LEXICON CheckCaps
%<cap%>@R.CAP.on@:0@R.CAP.on@                                 OptionalHyp ;
@D.CAP@                                                       OptionalHyp ;

LEXICON OptionalHyp
0:%-                                                          CheckEnClitic ;
                                                              CheckEnClitic ;
LEXICON CheckEnClitic
%+aku%<prn%>%<enc%>%<p1%>%<sg%>%<pos%>@R.DERNOUN@:00ku00000@R.DERNOUN@                   CheckParticle ;
%+kamu%<prn%>%<enc%>%<p2%>%<sg%>%<pos%>@R.DERNOUN@:000mu00000@R.DERNOUN@                 CheckParticle ;
%+kamu%<prn%>%<enc%>%<p2%>%<sg%>%<pos%>@R.DERNOUN@:00kau00000@R.DERNOUN@                 CheckParticle ;
%+dia%<prn%>%<enc%>%<p3%>%<sg%>%<pos%>@R.DERNOUN@:0nya00000@R.DERNOUN@                   CheckParticle ;
!%+dia%<prn%>%<enc%>%<p3%>%<sg%>%<pos%>%<cap%>@R.DERNOUN@:%-Nya000000@R.DERNOUN@          CheckParticle ;  !! check this later
%+aku%<prn%>%<enc%>%<p1%>%<sg%>@R.VOICE@:00ku0000@R.VOICE@                               CheckParticle ;
%+kamu%<prn%>%<enc%>%<p2%>%<sg%>@R.VOICE@:000mu0000@R.VOICE@                             CheckParticle ;
%+kamu%<prn%>%<enc%>%<p2%>%<sg%>@R.VOICE@:00kau0000@R.VOICE@                             CheckParticle ;
%+dia%<prn%>%<enc%>%<p3%>%<sg%>@R.VOICE@:0nya0000@R.VOICE@                               CheckParticle ;
!%+dia%<prn%>%<enc%>%<p3%>%<sg%>%<pos%>%<cap%>@R.VOICE@:%-Nya000000@R.VOICE@              CheckParticle ;  !! check this later
                                                                                         CheckParticle ;

LEXICON CheckParticle
%<par%>00:kah                   # ;
%<par%>00:lah                   # ;
%<par%>00:tah                   # ;
%<par%>00:pun                   # ;
                                # ;


!!! -- Monolingual Dictionary --
!!! 
!!! 
!!! ----------------------------------------------------------------

LEXICON Verb
tulis@P.POS.v@        InflectionOption ;
kirim@P.POS.v@        InflectionOption ;
dukung@P.POS.v@        InflectionOption ;
lari@P.POS.v@           InflectionOption ;

LEXICON Noun
buku@P.POS.n@             InflectionOption ;
toko@P.POS.n@         InflectionOption ;
karakter@P.POS.n@         InflectionOption ;
batas@P.POS.n@         InflectionOption ;
teks@P.POS.n@         InflectionOption ;
perangkat@P.POS.n@         InflectionOption ;
pesan@P.POS.n@         InflectionOption ;
hitam@P.POS.n@         InflectionOption ;
ibu@P.POS.n@         InflectionOption ;
sapu@P.POS.n@         InflectionOption ;
patung@P.POS.n@         InflectionOption ;
topi@P.POS.n@         InflectionOption ;

LEXICON Adjective
besar@P.POS.adj@        InflectionOption ;
hitam@P.POS.adj@        InflectionOption ;
lebih@P.POS.adj@        InflectionOption ;
cantik@P.POS.adj@        InflectionOption ;
dingin@P.POS.adj@        InflectionOption ;
cepat@P.POS.adj@        InflectionOption ;

LEXICON Numeral
satu@P.POS.num@              InflectionOption ;
dua@P.POS.num@               InflectionOption ;
tiga@P.POS.num@              InflectionOption ;
tujuh@P.POS.num@              InflectionOption ;

LEXICON Pronoun
saya@P.POS.prn@@P.PERSON.p1@@P.NUM.sg@                 InflectionOption ;
aku@P.POS.prn@@P.PERSON.p1@@P.NUM.sg@                  InflectionOption ;
anda@P.POS.prn@@P.PERSON.p2@@P.NUM.sg@                 InflectionOption ;
kamu@P.POS.prn@@P.PERSON.p2@@P.NUM.sg@                 InflectionOption ;
kamu@P.POS.prn@@P.PERSON.p2@@P.NUM.sg@:au00@P.POS.prn@@P.PERSON.p2@@P.NUM.sg@      InflectionOption ;
dia@P.POS.prn@@P.PERSON.p3@@P.NUM.sg@                  InflectionOption ;
kami@P.POS.prn@@P.PERSON.p1@@P.NUM.pl@                 InflectionOption ;
kita@P.POS.prn@@P.PERSON.p1@@P.NUM.pl@                 InflectionOption ;
kalian@P.POS.prn@@P.PERSON.p2@@P.NUM.pl@               InflectionOption ;
mereka@P.POS.prn@@P.PERSON.p3@@P.NUM.pl@               InflectionOption ;
beliau@P.POS.prn@@P.PERSON.p3@@P.NUM.sg@                 InflectionOption ;
0aku@P.POS.prn@@P.PERSON.p1@@P.NUM.sg@:daku@P.POS.prn@@P.PERSON.p1@@P.NUM.sg@      InflectionOption ;
0kamu@P.POS.prn@@P.PERSON.p2@@P.NUM.sg@:dikau@P.POS.prn@@P.PERSON.p2@@P.NUM.sg@    InflectionOption ;

LEXICON Subordinating
agar@P.POS.cnjsub@          InflectionOption ;
andai@P.POS.cnjsub@          InflectionOption ;
apabila@P.POS.cnjsub@          InflectionOption ;
yang@P.POS.cnjsub@          InflectionOption ;

LEXICON Coordinating
atau@P.POS.cnjcoo@        InflectionOption ;
dan@P.POS.cnjcoo@        InflectionOption ;
nan@P.POS.cnjcoo@        InflectionOption ;

LEXICON Determiner
ini@P.POS.det@             InflectionOption ;

LEXICON Adverb
ada@P.POS.adv@        InflectionOption ;
adakala@P.POS.adv@        InflectionOption ;
agak@P.POS.adv@        InflectionOption ;
alih%-alih@P.POS.adv@        InflectionOption ;
amat@P.POS.adv@        InflectionOption ;
apalagi@P.POS.adv@        InflectionOption ;
bagai@P.POS.adv@        InflectionOption ;
belum@P.POS.adv@              InflectionOption ;
bukan@P.POS.adv@              InflectionOption ;
kagak@P.POS.adv@              InflectionOption ;
tak@P.POS.adv@                InflectionOption ;
tidak@P.POS.adv@              InflectionOption ;


LEXICON Modal
akan@P.POS.mod@        InflectionOption ;
bakal@P.POS.mod@        InflectionOption ;

LEXICON Interjection
aduh@P.POS.interjection@              InflectionOption ;
aduhai@P.POS.interjection@              InflectionOption ;
alamak@P.POS.interjection@              InflectionOption ;
alangkah@P.POS.interjection@              InflectionOption ;
amboi@P.POS.interjection@              InflectionOption ;


LEXICON Question
apa@P.POS.question@              InflectionOption ;
bagaimana@P.POS.question@              InflectionOption ;
beberapa@P.POS.question@              InflectionOption ;uan
berapa@P.POS.question@              InflectionOption ;
kapan@P.POS.question@              InflectionOption ;
kenapa@P.POS.question@              InflectionOption ;
mana@P.POS.question@              InflectionOption ;
siapa@P.POS.question@              InflectionOption ;

LEXICON Particle
lah%<par%>:lah0    # ;
kah%<par%>:kah0    # ;
tah%<par%>:tah0    # ;
pun%<par%>:pun0    # ;

LEXICON Preposition
di%<pre%>:di0      # ;
ke%<pre%>:ke0      # ;


!!! -- End -- !!!
