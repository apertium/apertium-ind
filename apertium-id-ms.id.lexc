! Indonesian Morphological Analyzer
! Modified by : Septina Dian Larasati (septina.larasati@gmail.com)
! Date        : 04 June 2010

! Morphotactic Rules

! <TAGS>, ^NASAL, and @THE.DIACRITIC.FLAG@

! Tag definitions
Multichar_Symbols
         %<v%>      ! Verb
         %<n%>      ! Noun
         %<a%>      ! Adjective
         %<num%>    ! Numerals
         %<prn%>    ! Pronoun
         %<cnjsub%> ! Subordinating Conjunction
         %<cnjcoo%> ! Coordinating Conjunction
         %<av%>     ! Active voice
         %<pv%>     ! Passive voice
         %<sg%>     ! Singular Mood
         %<pl%>     ! Plural Mood
         %<per%>    ! Definite Verb
         %<imper%>  ! imperinite Verb
         %<vbser%>  ! Verb "to be"
         %<vbhaver%>! Verb "to have"
         %<caus%>   ! Causative Transitivity
         %<appl%>   ! ApplicativeTransitivity
         %<cap%>    ! Capitals
         %<theme%>  ! ???
	 ^N
	 ^REHYPH
	 ^CAP
	 +
	 @P.VOICE.av@
	 @P.VOICE.pv@
	 @P.ASPECT.perf@
	 @P.ASPECT.imperf@
	 @P.VERBVAR.vbser@
	 @P.VERBVAR.vbhaver@
	 @P.VERBVAR.vbexp@
	 @P.POS.v@
	 @P.POS.n@
	 @P.POS.adj@
	 @P.POS.num@
	 @P.POS.prn@
	 @P.POS.cnjsub@
	 @P.POS.cnjcoo@
	 @P.POS.det@
	 @P.NUM.sg@
	 @P.NUM.pl@
	 @P.NUM.coll@
	 @P.NUM.ord@
	 @P.NUM.car@
	 @P.NUM.uni@
	 @P.ADJ.elev@
	 @P.ADJ.sup@
	 @P.REDUP.on@
	 @P.REDUP.onsuff@
	 @P.NOUN.ent@
	 @P.NOUN.act@
	 @P.NOUN.actor@
	 @P.OBJ.caus@
	 @P.OBJ.appl@
	 @P.CAP.on@
	 @P.MARK.on@
	 @P.MARK1.off@
	 @P.MARK2.off@
	 @P.MARK2.ke@
	 @P.MARK2.pen@
	 @P.PROCLITIC.pro@
	 @P.ENCLITIC.enc@
	 @P.CLITPARM1.p1@
	 @P.CLITPARM1.p2@
	 @P.CLITPARM1.p3@
	 @P.CLITPARM2.p1@
	 @P.CLITPARM2.p2@
	 @P.CLITPARM2.p3@


! Root node
LEXICON Root
<0:ku "@P.MARK.on@" "@P.PROCLITIC.pro@" "@P.CLITPARM1.p1@">                 PRE-Prefix ;
<0:kau "@P.PROCLITIC.pro@" "@P.CLITPARM1.p2@">                 PRE-Prefix ;
        PRE-Prefix ;

LEXICON PRE-Prefix
<"@P.MARK.on@" "@P.VOICE.av@" "@P.ASPECT.imperf@" [0 .x. m e "^N"]>                    Prefix ;
<0:di "@P.MARK.on@" "@P.VOICE.pv@" "@P.ASPECT.imperf@">                    Prefix ;
!<"@P.MARK1.off@">        Prefix ;
        Prefix ;

LEXICON Prefix
<[0 .x. b e r] "@P.MARK.on@" "@D.VOICE@" "@P.VOICE.av@" "@P.ASPECT.perf@">                 Stem ;
<[0 .x. b e r] "@P.MARK.on@" "@P.VERBVAR.vbhaver@">                 Stem ;
<[0 .x. b e r] "@P.MARK.on@" "@P.NUM.coll@">                 Stem ;
<[0 .x. p e r] "@P.MARK.on@" "@P.VERBVAR.vbser@">                 Stem ;
<[0 .x. k e] "@P.MARK.on@" "@P.MARK2.ke@" "@P.VERBVAR.vbexp@">                 Stem ;
<[0 .x. k e] "@P.MARK.on@" "@P.NUM.ord@">                 Stem ;
<[0 .x. t e r] "@P.MARK.on@" "@D.VOICE@" "@P.VOICE.pv@" "@P.ASPECT.perf@">                 Stem ;
<[0 .x. t e r] "@P.MARK.on@" "@R.POS.adj@" "@P.ADJ.sup@">                 Stem ;
<[0 .x. s e] "@P.MARK.on@" "@P.NUM.uni@">                 Stem ;
<"@P.MARK.on@" "@P.MARK2.pen@" "@P.NUM.sg@" "@P.NOUN.actor@" [0 .x. p e "^N"]>                    Stem ;
<"@D.MARK@" "@P.MARK.on@" "@P.MARK2.pen@" "@P.NUM.sg@" "@P.NOUN.actor@" "@P.CAP.on@" [0 .x. P e "^N"]>                    Stem ;
<"@P.MARK.on@" "@P.MARK2.pen@" "@P.NOUN.actor@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" p e "^N"]>                    Stem ;
<"@D.MARK@" "@P.MARK.on@" "@P.MARK2.pen@" "@P.NOUN.actor@" "@P.REDUP.on@" "@D.MARK@" "@P.CAP.on@" [0 .x. "^[" "[" "{" P e "^N"]>                    Stem ;
<"@P.REDUP.on@" [0 .x. "^[" "[" "{" ]>       Stem ;
!<"@P.MARK2.off@">        Stem ;
        Stem ;

LEXICON Stem
        Verb ;
        Verb2 ;
        Noun ;
        Adjective ;
        Numeral ;
        Subordinating ;
        Pronoun ;
        Determiner ;



LEXICON RedupOption
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@P.NUM.pl@" "@C.REDUP@">		Suffix ;
        Suffix ;

LEXICON Suffix
<[0 .x. a n "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@D.VOICE@" "@R.REDUP.on@" "@P.NUM.pl@" "@D.MARK2@" "@P.NOUN.ent@" "@C.REDUP@">		Enclitic ;
<0:an "@D.VOICE@" "@D.REDUP@" "@D.MARK2@" "@P.NOUN.ent@">                Enclitic ;
<[0 .x. a n "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@P.NUM.pl@" "@R.MARK2.pen@" "@P.NOUN.act@" "@C.REDUP@">		Enclitic ;
<0:an "@D.REDUP@" "@R.MARK2.pen@" "@P.NOUN.act@">                Enclitic ;
<[0 .x. a n "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@P.NUM.pl@" "@R.VERBVER.vbexp@" "@C.REDUP@">		Enclitic ;
<0:an "@D.REDUP@" "@R.VERBVAR.vbexp@">                Enclitic ;
<0:kan "@D.REDUP@" "@U.VOICE.av@" "@P.OBJ.caus@">                Enclitic ;
<0:i "@D.REDUP@" "@U.VOICE.av@" "@P.OBJ.appl@">                Enclitic ;
<"@D.REDUP@">                Enclitic ;

LEXICON Enclitic
<0:ku  "@P.ENCLITIC.enc@" "@P.CLITPARM2.p1@">                 ClosureCheck ;
<0:mu  "@P.ENCLITIC.enc@" "@P.CLITPARM2.p2@">                 ClosureCheck ;
<0:nya  "@P.ENCLITIC.enc@" "@P.CLITPARM2.p3@">                 ClosureCheck ;
                 ClosureCheck ;


Lexicon ClosureCheck
<[%<v%> .x. 0] "@R.POS.v@">                        CheckVoice ;
<[%<n%> .x. 0] "@R.POS.n@">                        CheckVoice ;
<[%<adj%> .x. 0] "@R.POS.adj@">	                   CheckVoice ;
<[%<num%> .x. 0] "@R.POS.num@">		           CheckVoice ;
<[%<prn%> .x. 0] "@R.POS.prn@">		           CheckVoice ;
<[%<cnjsub%> .x. 0] "@R.POS.cnjsub@">              CheckVoice ;
<[%<cnjcoo%> .x. 0] "@R.POS.cnjcoo@">	           CheckVoice ;
<[%<det%> .x. 0] "@R.POS.det@">	           CheckVoice ;

Lexicon CheckVoice
<[%<av%> .x. 0] "@R.VOICE.av@">		           CheckDef ;
<[%<pv%> .x. 0] "@R.VOICE.pv@">                    CheckDef ;
<"@D.VOICE@">                                      CheckDef ;

Lexicon CheckDef
<[%<perf%> .x. 0] "@R.ASPECT.perf@">		   CheckNounType ;
<[%<imperf%> .x. 0] "@R.ASPECT.imperf@">		   CheckNounType ;
<"@D.ASPECT@">                                       CheckNounType ;

Lexicon CheckNounType
<[%<ent%> .x. 0] "@R.NOUN.ent@">		   CheckVerbVariance ;
<[%<act%> .x. 0] "@R.NOUN.act@">		   CheckVerbVariance ;
<[%<actor%> .x. 0] "@R.NOUN.actor@">		   CheckVerbVariance ;
<"@D.NOUN@">                                       CheckVerbVariance ;

Lexicon CheckVerbVariance
<[%<vbser%> .x. 0] "@R.VERBVAR.vbser@">		   CheckObject ;
<[%<vbhaver%> .x. 0] "@R.VERBVAR.vbhaver@">        CheckObject ;
<[%<vbexp%> .x. 0] "@R.VERBVAR.vbexp@">        CheckObject ;
<"@D.VERBVAR@">                                    CheckObject ;

Lexicon CheckObject
<[%<caus%> .x. 0] "@R.OBJ.caus@">		   CheckAdj ;
<[%<appl%> .x. 0] "@R.OBJ.appl@">		   CheckAdj ;
<"@D.OBJ@">                                        CheckAdj ;

Lexicon CheckAdj
<[%<elev%> .x. 0] "@R.ADJ.elev@">                  CheckNum ;
<[%<sup%> .x. 0] "@R.ADJ.sup@">                    CheckNum ;
<"@D.ADJ@">                                        CheckNum ;


Lexicon CheckNum
<[%<sg%> .x. 0] "@R.NUM.sg@">           CheckProClitic ;
<[%<pl%> .x. 0] "@R.NUM.pl@">           CheckProClitic ;
<[%<coll%> .x. 0] "@R.NUM.coll@" "@R.POS.num@">       CheckProClitic ;
<[%<ord%> .x. 0] "@R.NUM.ord@" "@R.POS.num@">         CheckProClitic ;
<[%<car%> .x. 0] "@R.NUM.car@" "@R.POS.num@">         CheckProClitic ;
<[%<uni%> .x. 0] "@R.NUM.uni@">         CheckProClitic ;
<"@D.NUM@">                             CheckProClitic ;

Lexicon CheckProClitic
<[%<pro%>%<p1%> .x. 0] "@R.PROCLITIC.pro@" "@R.CLITPARM1.p1@" "@D.POS.cnjsub@" "@D.POS.cnjcoo@">                    CheckEnClitic ;
<[%<pro%>%<p2%> .x. 0] "@R.PROCLITIC.pro@" "@R.CLITPARM1.p2@" "@D.POS.cnjsub@" "@D.POS.cnjcoo@">                    CheckEnClitic ;
<[%<pro%>%<p3%> .x. 0] "@R.PROCLITIC.pro@" "@R.CLITPARM1.p3@" "@D.POS.cnjsub@" "@D.POS.cnjcoo@">                    CheckEnClitic ;
<"@D.PROCLITIC@">                                                                 CheckEnClitic ;

Lexicon CheckEnClitic
<[%<enc%>%<p1%> .x. 0] "@R.ENCLITIC.enc@" "@R.CLITPARM2.p1@" "@D.POS.cnjsub@" "@D.POS.cnjcoo@">                    CheckCaps ;
<[%<enc%>%<p2%> .x. 0] "@R.ENCLITIC.enc@" "@R.CLITPARM2.p2@" "@D.POS.cnjsub@" "@D.POS.cnjcoo@">                    CheckCaps ;
<[%<enc%>%<p3%> .x. 0] "@R.ENCLITIC.enc@" "@R.CLITPARM2.p3@" "@D.POS.cnjsub@" "@D.POS.cnjcoo@">                    CheckCaps ;
<"@D.ENCLITIC@">                                                                 CheckCaps ;

Lexicon CheckCaps
<[%<cap%> .x. 0] "@R.CAP.on@">           # ;
<"@D.CAP@">                              # ;





LEXICON Verb
< ["@D.MARK@" "@P.CAP.on@" t:T | t] ulis "@P.POS.v@">        RedupOption ;
< ["@D.MARK@" "@P.CAP.on@" k:K | k] irim "@P.POS.v@">        RedupOption ;
< ["@D.MARK@" "@P.CAP.on@" d:D | d] ukung "@P.POS.v@">       RedupOption ;

LEXICON Verb2
< ["@D.MARK@" "@P.CAP.on@" l:L | l] ari "@P.POS.v@">         RedupOption ;

LEXICON Noun
< ["@D.MARK@" "@P.CAP.on@" p:P | p] esan "@P.POS.n@">        RedupOption ;
< ["@D.MARK@" "@P.CAP.on@" p:P | p] es "@P.POS.n@">          RedupOption ; !toy
< ["@D.MARK@" "@P.CAP.on@" p:P | p] erangkat "@P.POS.n@">    RedupOption ;
< ["@D.MARK@" "@P.CAP.on@" t:T | t] eks "@P.POS.n@">         RedupOption ;
< ["@D.MARK@" "@P.CAP.on@" b:B | b] atas "@P.POS.n@">        RedupOption ;
< ["@D.MARK@" "@P.CAP.on@" k:K | k] arakter "@P.POS.n@">     RedupOption ;
< ["@D.MARK@" "@P.CAP.on@" t:T | t] oko "@P.POS.n@">         RedupOption ;
< ["@D.MARK@" "@P.CAP.on@" b:B | b] uku "@P.POS.n@">         RedupOption ;

LEXICON Adjective
< ["@D.MARK@" "@P.CAP.on@" b:B | b] esar "@P.POS.adj@">      RedupOption ;
< ["@D.MARK@" "@P.CAP.on@" l:L | l] ebih "@P.POS.adj@">      RedupOption ;

LEXICON Numeral
< ["@D.MARK@" "@P.CAP.on@" d:D | d] ua "@P.POS.num@">        RedupOption ;
< ["@D.MARK@" "@P.CAP.on@" t:T | t] iga "@P.POS.num@">       RedupOption ;


LEXICON Pronoun
< ["@D.MARK@" "@P.CAP.on@" a:A | a] nda "@P.POS.prn@">       RedupOption ;

LEXICON Subordinating
< ["@D.MARK@" "@P.CAP.on@" y:Y | y] ang "@P.POS.cnjsub@">    RedupOption ;

LEXICON Determiner
< ["@D.MARK@" "@P.CAP.on@" i:I | i] ni "@P.POS.det@">       RedupOption ;

