! Indonesian Morphological Analyzer
! Modified by : Septina Dian Larasati (septina.larasati@gmail.com)
! Date        : 04 June 2010

! Morphotactic Rules

! <TAGS>, ^NASAL, and @THE.DIACRITIC.FLAG@

! Tag definitions
Multichar_Symbols
         %<v%>      ! Verb
         %<n%>      ! Noun
         %<a%>      ! Adjective
         %<num%>    ! Numerals
         %<prn%>    ! Pronoun
         %<cnjsub%> ! Subordinating Conjunction
         %<cnjcoo%> ! Coordinating Conjunction
         %<av%>     ! Active voice
         %<pv%>     ! Passive voice
         %<sg%>     ! Singular Mood
         %<pl%>     ! Plural Mood
         %<per%>    ! Definite Verb
         %<imper%>  ! imperinite Verb
         %<vbser%>  ! Verb "to be"
         %<vbhaver%>! Verb "to have"
         %<caus%>   ! Causative Transitivity
         %<appl%>   ! ApplicativeTransitivity
         %<cap%>    ! Capitals
         %<theme%>  ! ???
	 ^N
	 ^REHYPH
	 ^CAP
	 +
	 @P.VOICE.av@
	 @P.VOICE.pv@
	 @P.ASPECT.perf@
	 @P.ASPECT.imperf@
	 @P.VERBVAR.vbser@
	 @P.VERBVAR.vbhaver@
	 @P.VERBVAR.vbexp@
	 @P.POS.v@
	 @P.POS.n@
	 @P.POS.adj@
	 @P.POS.num@
	 @P.POS.prn@
	 @P.POS.cnjsub@
	 @P.POS.cnjcoo@
	 @P.POS.det@
	 @P.NUM.sg@
	 @P.NUM.pl@
	 @P.NUM.coll@
	 @P.NUM.ord@
	 @P.NUM.car@
	 @P.NUM.uni@
	 @P.ADJ.elev@
	 @P.ADJ.sup@
	 @P.REDUP.on@
	 @P.REDUP.onsuff@
	 @P.NOUN.ent@
	 @P.NOUN.act@
	 @P.NOUN.actor@
	 @P.OBJ.caus@
	 @P.OBJ.appl@
	 @P.CAP.on@
	 @P.MARK.on@
	 @P.MARK1.off@
	 @P.MARK2.off@
	 @P.MARK2.ke@
	 @P.MARK2.pen@
	 @P.MARK2.ber@
	 @P.PROCLITIC.pro@
	 @P.ENCLITIC.enc@
	 @P.CLITPARM1.p1@
	 @P.CLITPARM1.p2@
	 @P.CLITPARM1.p3@
	 @P.CLITPARM2.p1@
	 @P.CLITPARM2.p2@
	 @P.CLITPARM2.p3@


! Root node
LEXICON Root
<aku%<pro%>%<p1%>%<sg%>%+:ku "@P.MARK.on@">               PRE-Prefix ;
<aku%<pro%>%<p1%>%<sg%>%<cap%>%+:Ku "@P.MARK.on@">        PRE-Prefix ;
<kau%<pro%>%<p2%>%<sg%>%+:kau "@P.MARK.on@">              PRE-Prefix ;
<kau%<pro%>%<p2%>%<sg%>%<cap%>%+:Kau "@P.MARK.on@">       PRE-Prefix ;
                                                          PRE-Prefix ;

LEXICON PRE-Prefix
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@P.MARK.on@"                         [0 .x. m e "^N"]>                  Prefix ;
<"@P.VOICE.av@" "@P.ASPECT.imperf@" "@D.MARK@" "@P.MARK.on@" "@P.CAP.on@" [0 .x. M e "^N"]>                  Prefix ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@P.MARK.on@"                         0:di>                              Prefix ;
<"@P.VOICE.pv@" "@P.ASPECT.imperf@" "@D.MARK@" "@P.MARK.on@" "@P.CAP.on@" 0:Di>                              Prefix ;
<["@D.MARK@" "@P.CAP.on@" 0:B | 0:b] 0:er "@P.MARK.on@" "@P.MARK2.ber@">                                     Stem ;
<"@P.VOICE.pv@" "@P.ASPECT.perf@"  ["@D.MARK@" "@P.CAP.on@" 0:T | 0:t] 0:er "@P.MARK.on@">                   Stem ;
<"@P.MARK2.pen@" "@P.NUM.sg@" "@P.NOUN.actor@" "@P.MARK.on@" [0 .x. p e "^N"]>                               Stem ;
<"@P.MARK2.pen@" "@P.NUM.sg@" "@P.NOUN.actor@" "@D.MARK@" "@P.MARK.on@" "@P.CAP.on@" [0 .x. P e "^N"]>       Stem ;
                                                                                                             Prefix ;

LEXICON Prefix
<["@D.MARK@" "@P.CAP.on@" 0:B | 0:b] 0:er "@P.MARK.on@"  "@P.MARK2.ber@">                                    Stem ;
<["@D.MARK@" "@P.CAP.on@" 0:P | 0:p] 0:er "@P.MARK.on@" "@P.VERBVAR.vbser@">                                 Stem ;
<["@D.MARK@" "@P.CAP.on@" 0:Ke | 0:ke]    "@P.MARK.on@" "@P.MARK2.ke@">                                      Stem ;
<["@D.MARK@" "@P.CAP.on@" 0:T | 0:t] 0:er "@P.MARK.on@" "@R.POS.adj@" "@P.ADJ.sup@">                         Stem ;
<["@D.MARK@" "@P.CAP.on@" 0:Se | 0:se]    "@P.MARK.on@" "@P.NUM.uni@">                                       Stem ;
<"@P.MARK2.pen@" "@P.NOUN.actor@" "@P.REDUP.on@" "@P.MARK.on@" [0 .x. "^[" "[" "{" p e "^N"]>                Stem ;
<"@D.MARK@" "@P.MARK2.pen@" "@P.NOUN.actor@" "@P.REDUP.on@" "@D.MARK@" "@P.CAP.on@" "@P.MARK.on@" [0 .x. "^[" "[" "{" P e "^N"]>      Stem ;
<"@P.REDUP.on@" [0 .x. "^[" "[" "{" ]>                                                                       Stem ;
                                                                                                             Stem ;

LEXICON Stem
        Verb ;
        Verb2 ;
        Noun ;
        Adjective ;
        Numeral ;
        Subordinating ;
        Pronoun ;
        Determiner ;



LEXICON RedupOption
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@P.NUM.pl@" "@C.REDUP@">		Suffix ;
                                                                                                Suffix ;

LEXICON Suffix
<[0 .x. a n "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@D.VOICE@" "@R.REDUP.on@" "@P.NUM.pl@" "@D.MARK2@" "@P.NOUN.ent@" "@C.REDUP@">		Enclitic ;
<0:an "@D.VOICE@" "@D.REDUP@" "@D.MARK2@" "@P.NOUN.ent@" "@P.NUM.sg@">                Enclitic ;
<[0 .x. a n "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@P.NUM.pl@" "@R.MARK2.pen@" "@P.NOUN.act@" "@C.REDUP@">		Enclitic ;
<0:an "@D.REDUP@" "@R.MARK2.pen@" "@P.NOUN.act@">                Enclitic ;
<[0 .x. a n "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.REDUP.on@" "@P.NUM.pl@" "@R.VERBVER.vbexp@" "@C.REDUP@">		Enclitic ;
<0:kan "@D.REDUP@" "@U.VOICE.av@" "@P.OBJ.caus@">                                Enclitic ;
<0:i "@D.REDUP@" "@U.VOICE.av@" "@P.OBJ.appl@">                                  Enclitic ;
<"@D.REDUP@" "@R.MARK2.ke@"  "@R.POS.adj@" "@P.VERBVAR.vbexp@" 0:an >            Enclitic ;
<"@D.REDUP@" "@R.MARK2.ke@"  "@R.POS.num@" "@P.NUM.ord@">                        Enclitic ;
<"@D.REDUP@" "@R.MARK2.ber@" "@R.POS.num@" "@P.NUM.coll@">                       Enclitic ;
<"@D.REDUP@" "@R.MARK2.ber@" "@R.POS.n@"   "@P.VERBVAR.vbhaver@">                Enclitic ;
<"@D.REDUP@" "@R.MARK2.ber@" "@R.POS.v@"   "@P.VOICE.av@" "@P.ASPECT.perf@">     Enclitic ;
<"@D.REDUP@" "@R.MARK2.pen@">                                                    Enclitic ;
<"@D.REDUP@" "@D.MARK2@">                                                        Enclitic ;



LEXICON Enclitic
<0:ku  "@P.ENCLITIC.enc@" "@P.CLITPARM2.p1@">      ClosureCheck ;
<0:mu  "@P.ENCLITIC.enc@" "@P.CLITPARM2.p2@">      ClosureCheck ;
<0:nya "@P.ENCLITIC.enc@" "@P.CLITPARM2.p3@">      ClosureCheck ;
                                                   ClosureCheck ;


Lexicon ClosureCheck
<[%<v%> .x. 0] "@R.POS.v@">                        CheckVoice ;
<[%<n%> .x. 0] "@R.POS.n@">                        CheckVoice ;
<[%<adj%> .x. 0] "@R.POS.adj@">	                   CheckVoice ;
<[%<num%> .x. 0] "@R.POS.num@">		           CheckVoice ;
<[%<prn%> .x. 0] "@R.POS.prn@">		           CheckVoice ;
<[%<cnjsub%> .x. 0] "@R.POS.cnjsub@">              CheckVoice ;
<[%<cnjcoo%> .x. 0] "@R.POS.cnjcoo@">	           CheckVoice ;
<[%<det%> .x. 0] "@R.POS.det@">	                   CheckVoice ;

Lexicon CheckVoice
<[%<av%> .x. 0] "@R.VOICE.av@">		           CheckDef ;
<[%<pv%> .x. 0] "@R.VOICE.pv@">                    CheckDef ;
<"@D.VOICE@">                                      CheckDef ;

Lexicon CheckDef
<[%<perf%> .x. 0] "@R.ASPECT.perf@">		   CheckNounType ;
<[%<imp%> .x. 0] "@R.ASPECT.imperf@">		   CheckNounType ;
<"@D.ASPECT@">                                     CheckNounType ;

Lexicon CheckNounType
<[%<ent%> .x. 0] "@R.NOUN.ent@">		   CheckVerbVariance ;
<[%<act%> .x. 0] "@R.NOUN.act@">		   CheckVerbVariance ;
<[%<actor%> .x. 0] "@R.NOUN.actor@">		   CheckVerbVariance ;
<"@D.NOUN@">                                       CheckVerbVariance ;

Lexicon CheckVerbVariance
<[%<vbser%> .x. 0] "@R.VERBVAR.vbser@">		   CheckObject ;
<[%<vbhaver%> .x. 0] "@R.VERBVAR.vbhaver@">        CheckObject ;
<[%<vbexp%> .x. 0] "@R.VERBVAR.vbexp@">            CheckObject ;
<"@D.VERBVAR@">                                    CheckObject ;

Lexicon CheckObject
<[%<caus%> .x. 0] "@R.OBJ.caus@">		   CheckAdj ;
<[%<appl%> .x. 0] "@R.OBJ.appl@">		   CheckAdj ;
<"@D.OBJ@">                                        CheckAdj ;

Lexicon CheckAdj
<[%<elev%> .x. 0] "@R.ADJ.elev@">                  CheckNum ;
<[%<sup%> .x. 0] "@R.ADJ.sup@">                    CheckNum ;
<"@D.ADJ@">                                        CheckNum ;


Lexicon CheckNum
<[%<sg%>   .x. 0] "@R.NUM.sg@">                    CheckCaps ;
<[%<pl%>   .x. 0] "@R.NUM.pl@">                    CheckCaps ;
<[%<coll%> .x. 0] "@R.NUM.coll@" "@R.POS.num@">    CheckCaps ;
<[%<ord%>  .x. 0] "@R.NUM.ord@" "@R.POS.num@">     CheckCaps ;
<[%<car%>  .x. 0] "@R.NUM.car@" "@R.POS.num@">     CheckCaps ;
<[%<uni%>  .x. 0] "@R.NUM.uni@">                   CheckCaps ;
<"@D.NUM@">                                        CheckCaps ;


Lexicon CheckCaps
<[%<cap%> .x. 0] "@R.CAP.on@">                     CheckEnClitic ;
<"@D.CAP@">                                        CheckEnClitic ;

Lexicon CheckEnClitic
<[%+aku%<enc%>%<p1%>%<sg%> .x. 0] "@R.ENCLITIC.enc@" "@R.CLITPARM2.p1@" "@D.POS.cnjsub@" "@D.POS.cnjcoo@">   # ;
<[%+kau%<enc%>%<p2%>%<sg%> .x. 0] "@R.ENCLITIC.enc@" "@R.CLITPARM2.p2@" "@D.POS.cnjsub@" "@D.POS.cnjcoo@">   # ;
<[%+dia%<enc%>%<p3%>%<sg%> .x. 0] "@R.ENCLITIC.enc@" "@R.CLITPARM2.p3@" "@D.POS.cnjsub@" "@D.POS.cnjcoo@">   # ;
<"@D.ENCLITIC@">                                                                                             # ;






LEXICON Verb
<[t:T "@D.MARK@" "@P.CAP.on@" | t] ulis "@P.POS.v@">        RedupOption ;
<[k:K "@D.MARK@" "@P.CAP.on@" | k] irim "@P.POS.v@">        RedupOption ;
<[d:D "@D.MARK@" "@P.CAP.on@" | d] ukung "@P.POS.v@">       RedupOption ;

LEXICON Verb2
<[l:L "@D.MARK@" "@P.CAP.on@" | l] ari "@P.POS.v@">         RedupOption ;

LEXICON Noun
<[p:P "@D.MARK@" "@P.CAP.on@" | p] esan "@P.POS.n@">        RedupOption ;
<[p:P "@D.MARK@" "@P.CAP.on@" | p] es "@P.POS.n@">          RedupOption ; !toy
<[p:P "@D.MARK@" "@P.CAP.on@" | p] erangkat "@P.POS.n@">    RedupOption ;
<[t:T "@D.MARK@" "@P.CAP.on@" | t] eks "@P.POS.n@">         RedupOption ;
<[b:B "@D.MARK@" "@P.CAP.on@" | b] atas "@P.POS.n@">        RedupOption ;
<[k:K "@D.MARK@" "@P.CAP.on@" | k] arakter "@P.POS.n@">     RedupOption ;
<[t:T "@D.MARK@" "@P.CAP.on@" | t] oko "@P.POS.n@">         RedupOption ;
<[b:B "@D.MARK@" "@P.CAP.on@" | b] uku "@P.POS.n@">         RedupOption ;

LEXICON Adjective
<[b:B "@D.MARK@" "@P.CAP.on@" | b] esar "@P.POS.adj@">      RedupOption ;
<[l:L "@D.MARK@" "@P.CAP.on@" | l] ebih "@P.POS.adj@">      RedupOption ;

LEXICON Numeral
<[d:D "@D.MARK@" "@P.CAP.on@" | d] ua "@P.POS.num@">        RedupOption ;
<[t:T "@D.MARK@" "@P.CAP.on@" | t] iga "@P.POS.num@">       RedupOption ;


LEXICON Pronoun
<[a:A "@D.MARK@" "@P.CAP.on@" | a] nda "@P.POS.prn@">       RedupOption ;

LEXICON Subordinating
<[y:Y "@D.MARK@" "@P.CAP.on@" | y] ang "@P.POS.cnjsub@">    RedupOption ;

LEXICON Determiner
<[i:I "@D.MARK@" "@P.CAP.on@" | i] ni "@P.POS.det@">        RedupOption ;

