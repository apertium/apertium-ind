! Modified by : Septina Dian Larasati (septina.larasati@gmail.com)
! Date        : 01 June 2010

! Morphotactic Rules

! <TAGS>, ^NASAL, and @THE.DIACRITIC.FLAG@

! Tag definitions
Multichar_Symbols
         %<v%>      ! Verb
         %<n%>      ! Noun
         %<a%>      ! Adjective
         %<num%>    ! Numerals
         %<prn%>    ! Pronoun
         %<cnjsub%> ! Subordinating Conjunction
         %<cnjcoo%> ! Coordinating Conjunction
         %<av%>     ! Active voice
         %<pv%>     ! Passive voice
         %<sg%>     ! Singular Mood
         %<pl%>     ! Plural Mood
         %<caus%>   ! Causative Transitivity
         %<appl%>   ! ApplicativeTransitivity
	 ^N
	 ^REHYPH
	 +
	 @P.VERB.av@
	 @P.VERB.pv@
	 @P.POS.v@
	 @P.POS.n@
	 @P.POS.adj@
	 @P.POS.num@
	 @P.POS.prn@
	 @P.POS.cnjsub@
	 @P.POS.cnjcoo@
	 @P.NUM.sg@
	 @P.NUM.pl@
	 @P.MARK.person@
	 @P.MARK.entity@
	 @P.MARK.process@
	 @P.MARK.regular@
	 @P.NOUN.entity@
	 @P.SUFF.on@
	 @P.SUFF.off@
	 @P.REDUP.on@
	 
         
! Root node
LEXICON Root
      PRE-Null ;
      PRE-Active ;
      PRE-Passive ;
      PRE-DerNoun ;

LEXICON PRE-Null
<"@P.VERB.av@">        Verb ;
<"@P.VERB.av@">        Verb2 ;
<"@P.VERB.av@" [0 .x. ber]>        Verb2 ;
<"@P.VERB.av@" "@P.NUM.pl@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" ]>        Verb ;
<"@P.VERB.av@" "@P.NUM.pl@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" ]>        Verb2 ;
<"@P.VERB.av@" "@P.NUM.pl@" "@P.REDUP.on@" [0 .x. ber "^[" "[" "{" ]>        Verb2 ;
<"@P.MARK.regular@" "@P.NUM.sg@">        Verb ;
<"@P.MARK.regular@" "@P.NUM.sg@">        Noun ;
<"@P.MARK.regular@" "@P.NUM.sg@">        Adjective ;
<"@P.NUM.pl@" "@P.REDUP.on@" [0 .x. "^[" "[" "{" ]>        Noun ;
        Adjective ;
        Numeral ;
        Pronoun ;
        Subordinating ;
        Coordinating ;


LEXICON PRE-Active
<"@P.VERB.av@" [0 .x. m e "^N"]>   LexGroup1 ;


LEXICON PRE-Passive
<"@P.VERB.pv@" 0:di>   LexGroup1 ;

LEXICON PRE-DerNoun
<"@P.MARK.person@" "@P.NUM.sg@" [0 .x. p e "^N"]>   Verb ;      !check: simplified!
<"@P.MARK.person@" "@P.NUM.sg@" [0 .x. p e "^N"]>   Noun ;      !check: simplified!
<"@P.MARK.person@" "@P.NUM.sg@" [0 .x. p e "^N"]>   Adjective ; !check: simplified!



LEXICON RedupBranch
<"@R.SUFF@" "@D.REDUP@" "@R.VERB@">		MoodSuffix ;
<"@D.SUFF.on@""@D.REDUP@" "@R.VERB@">        ClosureCheck ;
<[0 .x. "}" "%^REHYPH" "]" "^" 2 "^]"  ] "@R.NUM.pl@">		ClosureCheck ;
<"@R.MARK@" >		  DerNounSuffix ;
<["@R.POS.prn@" | "@R.POS.num@" | "@R.POS.adj@" | "@R.POS.cnjsub@" | "@R.POS.cnjcoo@"] "@D.VERB@" "@D.MARK@">		ClosureCheck ;

LEXICON MoodSuffix
<"@P.MOOD.caus@" 0:kan>   ClosureCheck ;
<"@P.MOOD.appl@" 0:i>   ClosureCheck ;

LEXICON DerNounSuffix
<"@R.MARK.person@">   ClosureCheck ;
<"@R.MARK.regular@" "@R.POS.n@">   ClosureCheck ;
<"@R.MARK.person@" "@P.MARK.process@" 0:an>   ClosureCheck ;
<"@R.MARK.regular@" "@P.MARK.entity@" 0:an>   ClosureCheck ;


! Lexical entry
LEXICON LexGroup1
<"@P.SUFF.off@">       Verb ;
<"@P.SUFF.on@">        Verb2 ;
<"@P.SUFF.off@">       Noun ;
<"@P.SUFF.off@">        Adjective ;
<"@P.SUFF.off@">        Numeral ;




Lexicon ClosureCheck
<[%<v%> .x. 0] "@R.POS.v@">		CheckVoice ;
<[%<n%> .x. 0] "@R.POS.n@">		CheckVoice ;
<[%<adj%> .x. 0] "@R.POS.adj@">		CheckVoice ;
<[%<num%> .x. 0] "@R.POS.num@">		CheckVoice ;
<[%<prn%> .x. 0] "@R.POS.prn@">		CheckVoice ;
<[%<cnjsub%> .x. 0] "@R.POS.cnjsub@">	CheckVoice ;
<[%<cnjcoo%> .x. 0] "@R.POS.cnjcoo@">	CheckVoice ;

Lexicon CheckVoice
<[%<av%> .x. 0] "@R.VERB.av@">		   CheckNum ;
<[%<pv%> .x. 0] "@R.VERB.pv@">             CheckNum ;
<[%<entity%> .x. 0] "@R.MARK.entity@">             CheckNum ;
<[%<process%> .x. 0] "@R.MARK.process@">             CheckNum ;
<[%<person%> .x. 0] "@R.MARK.person@">             CheckNum ;
<[%<regular%> .x. 0] "@R.MARK.regular@">             CheckNum ;
<"@D.VERB@" "@D.MARK@">   CheckNum ;

Lexicon CheckNum
<[%<sg%> .x. 0] "@R.NUM.sg@">		CheckTrans ;
<[%<pl%> .x. 0] "@R.NUM.pl@">		CheckTrans ;
<"@D.NUM@">		CheckTrans ;

Lexicon CheckTrans
<[%<trans%>%<caus%> .x. 0] "@R.MOOD.caus@">		# ;
<[%<trans%>%<appl%> .x. 0] "@R.MOOD.appl@">		# ;
<"@D.MOOD@">		# ;





LEXICON Verb
tulis@P.POS.v@      RedupBranch ;
kirim@P.POS.v@      RedupBranch ;
dukung@P.POS.v@     RedupBranch ;

LEXICON Verb2
lari@P.POS.v@       RedupBranch ;

LEXICON Noun
pesan@P.POS.n@      RedupBranch ;
pes@P.POS.n@      RedupBranch ; !toy
perangkat@P.POS.n@  RedupBranch ;
teks@P.POS.n@       RedupBranch ;
batas@P.POS.n@      RedupBranch ;
karakter@P.POS.n@   RedupBranch ;

LEXICON Adjective
berat@P.POS.adj@    RedupBranch ;
lebih@P.POS.adj@    RedupBranch ;

LEXICON Numeral
dua@P.POS.num@      RedupBranch ;

LEXICON Pronoun
anda@P.POS.prn@     RedupBranch ;

LEXICON Subordinating
yang@P.POS.cnjsub@  RedupBranch ;

